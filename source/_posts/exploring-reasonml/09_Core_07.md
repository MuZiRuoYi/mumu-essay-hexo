---
title: 探索ReasonML_09 - Variant类型
date: 2018-06-09 11:22:20
tags: [ReasonML]
category: 'reasonml'
---

[origin_url]: http://reasonmlhub.com/exploring-reasonml/ch_variants.html
[bucklescript_js_option]: https://bucklescript.github.io/bucklescript/api/Js.Option.html
[bucklescript_js_result]: https://bucklescript.github.io/bucklescript/api/Js.Result.html

_variant 类型_（Variant types，简写为*variants*）是被许多函数式语言所支持的一种数据类型。他们是 ReasonML 的重要组成部分，但是在 C 类语言（C，C++，Java，C#等）中是不可用的。这个章节将要详述 Variant 如何使用。

### variant 作为符号集（枚举）

variant 可以让你定义一个符号集。当你这样使用的时候，他们与 C 类语言中的枚举类似。如下所示，`type color`定义了六种颜色类型：

```OCaml
type color = Red | Orange | Yellow | Green | Blue | Purple;
```

这个类型定义中有两个元素：

- 类型名字，`color`，必须以小写字母开头。

- _构造函数（constructors）_（`Red`，`Orange`，...）必须以大写字母开头，当我们使用 variant 作为数据结构时，那么为什么我们称它为构造函数也就变得很明了了。

构造函数的名称在当前范围内必须是唯一的，这样可以让 ReasonML 很容易推断出他们的类型：

```OCaml
# Purple;
- : color = Purple
```

variant 可以通过`switch`和模式匹配来处理：

```OCaml
let invert = (c: color) =>
  switch c {
  | Red => Green
  | Orange => Blue
  | Yellow => Purple
  | Green => Red
  | Blue => Orange
  | Purple => Yellow
  };
```

这里，构造函数既是模式（=>的左边），也是值（=>的右边），以下是`invert()`的使用：

```OCaml
# invert(Red);
- : color = Green
# invert(Yellow);
- : color = Purple
```

#### 技巧：用 variant 代替布尔值

在 ReasonML 中，variant 往往是比布尔更好的选择。以如下函数定义为例。（记住，在 ReasonML 中，主参数在结尾处，以启动柯里化。）

```OCaml
let stringOfContact(includeDetails: bool, c: contact) => ···;
```

如下调用`stringOfContact`：

```OCaml
let str = stringOfContact(true, myContact);
```

目前还不清楚末尾的布尔值做了什么，我们可以通过标记参数来优化这个函数。

```OCaml
let stringOfContact(~includeDetails: bool, c: contact) => ···;
let str = stringOfContact(~includeDetails=true, myContact);
```

更为自描述性的做法是引入一个 variant 作为`~includeDetails`的值：

```OCaml
type includeDetails = ShowEverything | HideDetails;
let stringOfContact(~levelOfDetail: includeDetails, c: contact) => ···;
let str = stringOfContact(~levelOfDetail=ShowEverything, myContact);
```

使用 variant`includeDetails`有两点优势：

- “not showing details”意味着什么是非常清晰明了的

- 后续很容易添加更多的模式

#### 将变量值与数据关联起来

有时，你想使用 variant 值作为查找数据的键，有一种方法是通过一个将 variant 值映射到数据的函数来实现：

```OCaml
type color = Red | Orange | Yellow | Green | Blue | Purple;
let stringOfColor(c: color) =>
  switch c {
  | Red => "Red"
  | Orange => "Orange"
  | Yellow => "Yellow"
  | Green => "Green"
  | Blue => "Blue"
  | Purple => "Purple"
  };
```

### Variant 作为数据结构

每个构造函数可以保存一个或多个值，这些值通过位置来识别。也就是，各个构造函数类似于元组，下面代码展示了这个特性：

```OCaml
type point = Point(float, float);
type shape =
  | Rectangle(point, point)
  | Circle(point, float);
```

`point`类型是一个具有单个构造函数的 variant，它包含了两个浮点数，`shape`是另外一个 variant，是以下之一：

- 一个由两个顶点定义的`Rectangle`，或
- 一个由中心和半径定义的`Circle`

以下是如何使用构造函数：

    # let bottomLeft = Point(-1.0, -2.0);
    let bottomLeft: point = Point(-1., -2.);
    # let topRight = Point(7.0, 6.0);
    let topRight: point = Point(7., 6.);
    # let circ = Circle(topRight, 5.0);
    let circ: shape = Circle(Point(7., 6.), 5.);
    # let rect = Rectangle(bottomLeft, topRight);
    let rect: shape = Rectangle(Point(-1., -2.), Point(7., 6.));

由于每个构造函数的名称都是惟一的，所以 ReasonML 可以很容易的推断出这些类型。

如果构造函数处理数据，则通过`switch`来做模式匹配更为方便，因为它也可以访问数据。

```OCaml
let pi = 4.0 *. atan(1.0);

let computeArea = (s: shape) =>
  switch s {
  | Rectangle(Point(x1, y1), Point(x2, y2)) =>
    let width = abs_float(x2 -. x1);
    let height = abs_float(y2 -. y1);
    width *. height;
  | Circle(_, radius) => pi *. (radius ** 2.0)
  };
```

让我们使用`computeArea`来继续我们在 rtop 上的操作：

    # computeArea(circ);
    - : float = 78.5398163397448315
    # computeArea(rect);
    - : float = 64.

### 通过 variant 的自递归数据结构

你也可以通过 variant 定义自递归的数据结构。例如，包含整数节点的二叉树：

```OCaml
type intTree =
  | Empty
  | Node(int, intTree, intTree);
```

`intTree`的构造函数如下所示：

```OCaml
let myIntTree = Node(1,
  Node(2, Empty, Empty),
  Node(3,
    Node(4, Empty, Empty),
    Empty
  )
);
```

`myIntTree`看一来如下所示：1 有 2 和 3 两个字节点，2 有两个空字节点等等。

    1
      2
        X
        X
      3
        4
          X
          X
        X

#### 使用 variant 处理自递归数据结构

为了演示自递归数据结构的处理，让我们设计一个`computeSum`函数，用来计算节点上整数的和。

```OCaml
let rec computeSum = (t: intTree) =>
  switch t {
  | Empty => 0
  | Node(i, leftTree, rightTree) =>
    i + computeSum(leftTree) + computeSum(rightTree)
  };

computeSum(myIntTree); /* 10 */
```

这是一种使用 variant 类型的典型递归：

1.  使用一组有限的构造函数来创建数据，在这种情况下：`Empty`和`Node()`。
2.  使用相同的构造函数用作数据处理的模式。

这样确保我们能够正确的处理传递给我们的任何`intTree`类型数据。如果`switch`不能完全覆盖`intTree`的所有情况，ReasonML 会发出警告，这样能够避免我们遗漏一些应该考虑到的情况。为了证明，假设如下`computeSum`函数，我们漏写了`Empty`情况：

```OCaml
let rec computeSum = (t: intTree) =>
  switch t {
  /* Missing: Empty */
  | Node(i, leftTree, rightTree) =>
    i + computeSum(leftTree) + computeSum(rightTree)
  };
```

所以我们会得到如下警告：

    Warning: this pattern-matching is not exhaustive.
    Here is an example of a value that is not matched:
    Empty

正如函数章节所提到的，引入所有的能捕获情况，意味着你失去了这种保护。这也是你为什么应该进行避免他们的原因。

#### 使用 variant 处理相互递归数据结构

回想一下`let`，无论何时，我们得使用`let rec`来定义一个递归：

- 通过`let rec`来定义一个单一的自递归。
- 通过`let rec`来定义并且通过`and`来连接多个互递归。

`type`是隐式的`rec`，它允许我们定义一个自递归，比如`intTree`。对于互递归的定义，我们还需要使用`and`把多个定义关联起来。如下例，再次定义`intTree`，但是这次我们把节点当作了一个单独的类型。

```OCaml
type intTree =
  | Empty
  | IntTreeNode(intNode)
and intNode =
  | IntNode(int, intTree, intTree);
```

因为`intTree`和`intNode`互递归，所以他们需要在同一个类型声明中来定义，并且通过`and`把两个定义隔离开。

### 参数化的 variant

让我们回忆一下`intTree`的原始定义：

```OCaml
type intTree =
  | Empty
  | Node(int, intTree, intTree);
```

我们如何将这个定义变成树的泛型定义，并且节点能够包含任意类型的值？因此我们不得不为节点内容引入类型变量。在 ReasonML 中使用撇号前缀标记*类型变量（Type variables）*。比如：`'a`。因此，如下定义泛型树：

```OCaml
type tree('a) =
  | Empty
  | Node('a, tree('a), tree('a));
```

有两件事值得注意。首先，之前`Node`的内容是`int`类型，现在是`'a`类型；然后，类型变量`'a`已经变成了一个参数，`Node`将该参数传递给子树。也就是说，我们可以为每个树选择不同的节点数据类型，但是再一个树中，所有的节点必须是相同的数据类型。

现在，我们可以使用`type`别名，通过树的类型参数来定义一个整型的树：

```OCaml
type intTree = tree(int);
```

我们通过树的定义来创建一个字符串类型的树：

```OCaml
let myStrTree = Node("a",
  Node("b", Empty, Empty),
  Node("c",
    Node("d", Empty, Empty),
    Empty
  )
);
```

由于类型推断，不需要提供类型参数。ReasonML 通过自动的类型推断来推断出`myStrTree`的类型`tree(string)`。如下泛型函数可以打印任何类型的树：

```OCaml
/**
 * @param ~indent How much to indent the current (sub)tree.
 * @param ~stringOfValue Converts node values to strings.
 * @param t The tree to convert to a string.
 */
let rec stringOfTree = (~indent=0, ~stringOfValue: 'a => string, t: tree('a)) => {
  let indentStr = String.make(indent*2, ' ');
  switch t {
  | Empty => indentStr ++ "X" ++ "\n"
  | Node(x, leftTree, rightTree) =>
    indentStr ++ stringOfValue(x) ++ "\n" ++
    stringOfTree(~indent=indent+1, ~stringOfValue, leftTree) ++
    stringOfTree(~indent=indent+1, ~stringOfValue, rightTree)
  };
};
```

这个函数使用递归来对参数 t 的节点进行迭代。如果`stringOfTree`与任意类型的`'a`在一起使用，我们需要一个特殊的类型转换函数来将`'a`转换成字符串，这也是使用`~stringOfValue`的原因。

如下打印出我们之前定义的`myStrTree`：

    # print_string(stringOfTree(~stringOfValue=x=>x, myStrTree));
    a
      b
        X
        X
      c
        d
          X
          X
        X

### 使用标准 variant

我将简要介绍两个常用的标准 variant。

#### 可选值的 option('a)类型

在许多面向对象语言中，具有类型字符串的变量意味着变量可以是空值（`null`）也可以是字符串值。包含空值的类型叫做*可空类型*，因为你很容易处理值而忘记处理空值，所以可空类型容易出现问题。如果意外出现了空值，可能你就会遇到了臭名昭著的空指针异常。

在 ReasonML 中，类型永远是可空的。相反的，潜在的缺失值问题会通过如下的参数化的 variant 来处理：

```OCaml
type option('a) =
  | None
  | Some('a);
```

`option`强制你去考虑`None`的情况。

ReasonML 对`option`有极少的支持。variant 的定义是语言的一部分，但是核心标准库还没有用于使用可选值（optional values）的实用函数。在实现之前，你可以使用 BuckleScript 的[Js.Option][bucklescript_js_option]。

#### Type result('a)的错误处理

`result`是在 OCaml 中错误处理的另外一个标准的 variant：

```OCaml
type result('good, 'bad) =
  | Ok('good)
  | Error('bad);
```

除非 ReasonML 的核心库支持它，否则你可以使用 BuckleScript 的[Js.Result][bucklescript_js_result]。

#### 示例：整型表达式求值

使用树，是 ML 风格语言的优点之一，这就是为什么他们被用到涉及语法树（解释器、编译器等）的程序中。例如，Facebook 的 Flow 语法检查程序就是使用 OCaml 写的。

因此，作为一个总结性的示例，让我们实现一个简单的整型表达式求值器。

下面是整数表达式的数据结构。

```OCaml
type expression =
  | Plus(expression, expression)
  | Minus(expression, expression)
  | Times(expression, expression)
  | DividedBy(expression, expression)
  | Literal(int);
```

使用 variant 编码的表达式，看起来如下所示：

```OCaml
/* (3 - (16 / (6 + 2)) */
let expr =
  Minus(
    Literal(3),
    DividedBy(
      Literal(16),
      Plus(
        Literal(6),
        Literal(2)
      )
    )
  );
```

最后，如下是整型表达式的求值函数。

```OCaml
let rec eval(e: expression) =
  switch e {
  | Plus(e1, e2) => eval(e1) + eval(e2)
  | Minus(e1, e2) => eval(e1) - eval(e2)
  | Times(e1, e2) => eval(e1) * eval(e2)
  | DividedBy(e1, e2) => eval(e1) / eval(e2)
  | Literal(i) => i
  };

eval(expr); /* 1 */
```

[阅读原文][origin_url]
