---
title: 探索ReasonML_13 - record
date: 2018-09-04 22:29:34
tags: [ReasonML]
category: 'reasonml'
---

[origin_url]: http://reasonmlhub.com/exploring-reasonml/ch_records.html
[config_warning]: https://bucklescript.github.io/docs/en/build-configuration.html#warnings

本章节探讨 ReasonML 的 record 是如何使用的。

### 什么是 record？

一个 record 类似于元组：他有固定的尺寸，每部分可以有不同的数据类型并且可以直接访问。在元组里（它的*组件（components）*）是可以通过位置来访问的，但是 record 的一部分（它的*字段（fields）*）是通过名字访问的。默认情况下，record 是不可变的。

### 基本用途

#### 定义 record 类型

定义 record 之前，必须为它定义一种类型，例如：

```OCaml
type point = {
  x: int,
  y: int, /* optional trailing comma */
};
```

我们已经定义了 record 类型`point`，它有两个字段`x`和`y`。字段名必须以小写字母开头。

在相同的作用域内，没有哪两个 record 应该具有相同的字段名，之所有有这个限制性规则，是由于字段名用于确定 record 的类型。为了做到这一点，每个字段名与一个 record 类型相关联。

在不止一个 record 中使用相同的字段名是允许的，但是可能性能会受到影响：有一个字段名为“wins”的最后一个 record，会有 w.r.t.的类型推断[[注 1](#note_1)]。因此，使用其他 record 类型会变的更加复杂。所以我宁愿假装重用字段名是不可能的。

稍后我们将围绕这个限制进行一些研究。

#### 嵌套 record 类型

是否可以嵌套 record 类型？例如，我们能做到以下几点吗？

    type t = { a: int, b: { c: int }};

不，我们不能，会有语法错误，一下才是`t`的正确定义：

    type b = { c: int };
    type t = { a: int, b: b };

对于`b: b`，这个字段的字段名与字段值是相同的，然后可以把两个在一起简写为`b`，这个被称为*双关语（punning）*：

    type t = { a: int, b };

#### 从零开始创建 record

如下是如何从零开始创建一个 record：

    # let pt1 = { x: 12, y: -2 };
    let pt1: point = {x: 12, y: -2};

请注意怎么通过字段名来推断出`pt1`的数据类型是`point`的。

双关也可以这么用：

```OCaml
let x = 7;
let y = 8;

let pt2 = {x, y};
  /* Same as: { x: x, y: y } */
```

#### 访问字段值

通过点（`.`）操作符访问字段值：

    # let pt = {x: 1, y: 2};
    let pt: point = {x: 1, y: 2};
    # pt.x;
    - : int = 1
    # pt.y;
    - : int = 2

#### record 的无损（non-destructive）更新

record 是不可变的，为了改变一个 record`r`的字段`f`，我们必须创建一个新的 record`s`，`s.f`有一个新的值，其他所有字段都与之前`r`的对应字段值相同。这个可以通过以下语法实现：

    let s = {...r, f: newValue}

三个点（`...`）被称作*扩展操作符（spread operator）*。它们必须放到最前面，并且最对只能使用一次。但是你一次可以更新多个字段值（而不仅仅是字段`f`）。

这是扩展操作符的一个实例：

    # let pt = {x: 1, y: 2};
    let pt: point = {x: 1, y: 2};
    # let pt' = {...pt, y: 3};
    let pt': point = {x: 1, y: 3};

#### 模式匹配

所有常见的模式匹配在 record 上也能使用，例如：

```OCaml
let isOrig = (pt: point) =>
  switch pt {
  | {x: 0, y: 0} => true
  | _ => false
  };
```

如下通过`let`：

    # let pt = {x: 1, y: 2};
    let pt: point = {x: 1, y: 2};

    # let {x: xCoord} = pt;
    let xCoord: int = 1;

也可以通过双关：

    # let {x} = pt;
    let x: int = 1;

在参数里也同样可是使用：

    # let getX = ({x}) => x;
    let getX: (point) => int = <fun>;
    # getX(pt);
    - : int = 1

##### 关于丢失字段的警告

在模式匹配时候，默认情况下，你可以省略掉所有你不感兴趣的字段，例如：

```OCaml
type point = {
  x: int,
  y: int,
};

let getX = ({x}) => x; /* Don’t do this */
```

在函数`getX()`里，我们对字段`y`不感兴趣并且只提到字段`x`。但是，最好明确的省略不关心的字段：

    let getX = ({x, _}) => x;

这个`x`后面的下划线告诉 ReasonML：我们忽略剩余的所有字段。

为什么明确点更好呢？因为现在可以在`bsconfig.json`中添加如下配置，当丢失字段名的时候，让 ReasonML 给出警告：

```json
"warnings": {
  "number": "+R"
}
```

现在，最初始的用法会触发警告：

    Warning number 9

    4 │ };
    5 │
    6 │ let getX = ({x}) => x;

    the following labels are not bound in this record pattern:
    y
    Either bind these labels explicitly or add '; _' to the pattern.

我建议更近一步，当缺失字段时候报错（不能完成编译）：

```json
"warnings": {
  "number": "+R",
  "error": "+R"
}
```

关于[配置警告的更多信息][config_warning]，请参阅 BuckleScript 手册。

对于使用所有当前字段的代码，检查遗漏字段显得尤为重要：

```OCaml
let string_of_point = ({x, y}: point) =>
  "(" ++ string_of_int(x) ++ ", "
  ++ string_of_int(y) ++ ")";

string_of_point({x:1, y:2});
  /* "(1, 2)" */
```

如果将另外一个字段添加到`point`（例如，`z`），则需要 ReasonML 给出与`string_of_point`有关的警告，以便对其进行修改。

#### 递归 record 类型

variant 是已经看到过的递归定义类型的第一个例子，record 也同样可以在递归中定义，例如：

```OCaml
type intTree =
  | Empty
  | Node(intTreeNode)
and intTreeNode = {
  value: int,
  left: intTree,
  right: intTree,
};
```

variant`intTree`递归式地依赖于 record 类型`intTreeNode`的定义，这是如何创建`intTree`类型元素：

```OCaml
let t = Node({
  value: 1,
  left: Node({
    value: 2,
    left: Empty,
    right: Node({
      value: 3,
      left: Empty,
      right: Empty,
    }),
  }),
  right: Empty,
});
```

#### 参数化的 record 类型

在 ReasonML 中，类型可以通过类型变量参数化，在定义 record 类型的时候可以使用这些类型变量。例如，如果希望一个树包含任意值，而不仅仅是 int 型，可以使字段的`value`是多态类型（行 A）：

```OCaml
type tree('a) =
  | Empty
  | Node(treeNode('a))
and treeNode('a) = {
  value: 'a, /* A */
  left: tree('a),
  right: tree('a),
};
```

### 其他模块的 record 类型

每一个模块都在某个作用域（例如一个模块）内定义，他的字段存在于顶层作用域，虽然这样有助于类型推断，但是它使得字段名比许多其他语言更加复杂。看看如果把`point`放到另外一个模块，各种 record 相关机制是受到什么影响，如`M`模块：

```OCaml
module M = {
  type point = {
    x: int,
    y: int,
  };
};
```

#### 从其他模块创建 record

如果我们试图创建一个`point`类型的 record，似乎和`point`类型在同一个作用域，但是我们失败了：

```OCaml
let pt = {x: 3, y: 2};
  /* Error: Unbound record field x */
```

原因是当前作用域范围内不存在`x`和`y`，他们仅存在于`M`模块。

通过至少限定一个字段名的作用域，可以解决这个问题：

```OCaml
let pt1 = {M.x: 3, M.y: 2}; /* OK */
let pt2 = {M.x: 3, y: 2}; /* OK */
let pt3 = {x: 3, M.y: 2}; /* OK */
```

另一种方法是限定整个 record。有趣的是，ReasonML 如何去报告这个推断类型——类型和第一个字段名都是过检测的：

    # let pt4 = M.{x: 3, y: 2};
    let pt4: M.point = {M.x: 3, y: 2};

最后，我们还可以 open`M`，因此`x`和`y`都被 import 到当前作用域：

```OCaml
open M;
let pt = {x: 3, y: 2};
```

#### 从其他模块访问字段

如果不 open`M`模块，则不能通过变量名随意访问字段：

```OCaml
let pt = M.{x: 3, y: 2};

print_int(pt.x);
/*
Warning 40: x was selected from type M.point.
It is not visible in the current scope, and will not
be selected if the type becomes unknown.
*/
```

如果限定了字段名则警告会消失：

    print_int(pt.M.x); /* OK */

就近 open`M`模块也可以：

```OCaml
M.(print_int(pt.x));
print_int(M.(pt.x));
```

#### 模式匹配与其他模块的 record

模式匹配，将于常规字段访问遇到相同的问题——如果不对字段名进行限定，则不能使用`point`的字段：

    # let {x, _} = pt;
    Error: Unbound record field x

如果限定了`x`，则一切正常：

    # let {M.x, _} = pt;
    let x: int = 3;

唉，有权限模式匹配也不能正常使用：

    # let M.{x, _} = pt;
    Error: Syntax error

我们可以把包含整个`let`绑定的地方进行局部 open`M`模块。然而，这不是一个表达式，阻止使用括号括起来，必须把这一块代码附加到一个代码块（大括号）里：

```OCaml
M.({
  let {x, _} = pt;
  ···
});
```

#### 在多个 record 中使用相同的字段名

最初说过，可以在多个 record 使用相同的字段名，这样做的诀窍就是把每个 record 放到单独的模块中。例如，有两个 record 类型，`Person.t`和`Plant.t`，这两个都有相同的`name`字段。但是，他们都是驻留在不同的模块中，所以名字冲突不是问题：

```OCaml
module Person = {
  type t = { name: string, age: int };
};
module Plant = {
  type t = { name: string, edible: bool };
};
```

### FAQ：record

#### 是否有方法动态指定字段名？

在 JavaScript 中，访问一个字段有两种方式（JavaScript 中称作*属性—，property*）：

```js
// Static field name (known at compile time)
console.log(obj.prop);

function f(obj, fieldName) {
  // Dynamic field name (known at runtime)
  console.log(obj[fieldName]);
}
```

在 ReasonML 中，字段名总是静态的。JavaScript 对象扮演两个角色：记录（records）和字典（dictionary）。在 ReasonML 中，需要记录就使用 record，需要字典就使用 Map。

<span id="note_1">【注 1】</span>原文为：The last record type with a field name “wins” w.r.t. type inference.

[阅读原文][origin_url]
