---
title: 探索ReasonML_08 - 基础模块
date: 2018-05-06 18:06:20
tags: [ReasonML]
category: 'reasonml'
---

[origin_url]: http://reasonmlhub.com/exploring-reasonml/ch_basic-modules.html
[reasonml_demo_modules]: https://github.com/rauschma/reasonml-demo-modules
[bucklescript_doc]: https://bucklescript.github.io/docs/en/automatic-interface-generation.html
[bucklescript_js_date]: https://bucklescript.github.io/bucklescript/api/Js.Date.html
[module_from]: https://blog.janestreet.com/better-namespaces-through-module-aliases/
[js_date]: https://bucklescript.github.io/bucklescript/api/Js.Date.html
[reasonml_api_doc]: https://reasonml.github.io/api/
[bucklescript_api_doc]: https://bucklescript.github.io/bucklescript/api/
[pervasives]: https://reasonml.github.io/api/Pervasives.html
[std_labels_module]: https://reasonml.github.io/api/StdLabels.html
[more_labels_module]: https://reasonml.github.io/api/MoreLabels.html
[bucklescript_build_sys_support]: https://bucklescript.github.io/bucklescript/Manual.html#_build_system_support
[reasonml_doc_find_npm_lib]: https://reasonml.github.io/docs/en/libraries.html

在本章中，我们将探讨模块在 ReasonML 中的运行方式。

### 安装演示库

本章节演示库在 GitHub：[reasonml-demo-modules][reasonml_demo_modules]。下载并安装：

    cd reasonml-demo-modules/
    npm install

这就是你需要做的——没有必要全局安装。

### 你的第一个 ReasonML 程序

这是你第一个 ReasonML 程序：

    reasonml-demo-modules/
        src/
            HelloWorld.re

在 ReasonML 中，每个扩展名为`.re`的文件都是一个模块。模块名是以大写字符开头的驼峰式命名。文件名定义了模块名，所以他们有相同的命名规则。

程序只是从命令行运行的模块。

`HelloWorld.re`看起来如下所示：

```OCaml
/* HelloWorld.re */

let () = {
  print_string("Hello world!");
  print_newline()
};
```

这些代码也许看起来有点奇怪，所以让我来解释一下：我们正在运行大括号内的两行代码，并且把他们的结果指定给模式`()`。也就是说，没有创建新的变量，但是模式可以确保结果是`()`。类型`()`，`unit`，类似于 C 型语言中的`void`。

请注意，我们没有定义函数，而是立即运行了`print_string()`和`print_newline()`两个函数。

想要编译这段代码，你有两个选择（请查看`package.json`中更多运行命令）：

* 一次性编译所有内容：`npm run build`

* 监控所有文件并且增量编译已经更改的文件：`npm run watch`

因此，我们的下一步是（在单独的终端窗口运行或者在后台运行接下来一步）：

    cd reasonml-demo-modules/
    npm run watch

在`HelloWorld.re`旁边有一个叫`HelloWorld.bs.js`的文件，你可以按照如下方式运行此文件。

    cd reasonml-demo-modules/
    node src/HelloWorld.bs.js

#### 其他版本的`HelloWorld.re`

作为一种替代方案（这是一种常见的 OCaml 公约），我们也可以把这两行代码简单的放入全局定义域：

```OCaml
/* HelloWorld.re */

print_string("Hello world!");
print_newline();
```

并且我们还可以定义一个 main()函数，然后调用：

```OCaml
/* HelloWorld.re */

let main = () => {
  print_string("Hello world!");
  print_newline()
};
main();
```

### 两个简单模块

让我们继续使用另外一个被`Main.re`使用的模块`MathTools.re`：

    reasonml-demo-modules/
        src/
            Main.re
            MathTools.re

`MathTools`模块如下所示：

```OCaml
/* MathTools.re */

let times = (x, y) => x * y;
let square = (x) => times(x, x);
```

`Main`模块如下所示：

```OCaml
/* Main.re */

let () = {
  print_string("Result: ");
  print_int(MathTools.square(3));
  print_newline()
};
```

正如你所看到的，在 ReasonML 中，你可以通过简单的通过模块名来使用模块，他们能够在项目中的任何位置被发现。

#### 子模块

你也可以嵌套模块，所以如下代码可以正常使用：

```OCaml
/* Main.re */

module MathTools = {
  let times = (x, y) => x * y;
  let square = (x) => times(x, x);
};

let () = {
  print_string("Result: ");
  print_int(MathTools.square(3));
  print_newline()
};
```

在其他模块，你可以通过`Main.MathTools`来访问`MathTools`。

让我们进一步进行嵌套：

```OCaml
/* Main.re */

module Math = {
  module Tools = {
    let times = (x, y) => x * y;
    let square = (x) => times(x, x);
  };
};

let () = {
  print_string("Result: ");
  print_int(Math.Tools.square(3));
  print_newline()
};
```

### 控制如何从模块导出值

默认情况下，模块的每一个子模块，类型，值都是可以导出的。如果你想要隐藏这些导出，你必须使用接口。此外，接口支持抽象类型（该类型内部结构是隐藏的）。

#### 接口文件

你可以根据所谓的接口来控制到处多少。对于模块定义文件`Foo.re`，你可以将接口放到文件`Foo.rei`中，例如：

```OCaml
/* MathTools.rei */

let times: (int, int) => int;
let square: (int) => int;
```

例如，你在接口文件中省略了`times`，它将不会被导出。

文件的接口文件也叫做它的*签名（signature）*。

如果存在接口文件，则必须把文档块注释放到接口文件里，否则，将他们放到`.re`文件里。

值得庆幸的是，我们不必手工编写接口，我们可以从模块中生成。如何生成在[BuckleScript 文档][bucklescript_doc]中有描述。对于`MathTools.rei`，我是这样做的：

    bsc -bs-re-out lib/bs/src/MathTools-ReasonmlDemoModules.cmi

#### 定义子模块接口

让我们假设，MathTools 没有在它自己的文件中，而是一个子模块：

```OCaml
    module MathTools = {
      let times = (x, y) => x * y;
      let square = (x) => times(x, x);
    };
```

我们如何定义这个模块的接口？我们有两种选择。

首先，我们可以通过`module type`定义和命名一个接口：

```OCaml
    module type MathToolsInterface = {
      let times: (int, int) => int;
      let square: (int) => int;
    };
```

该接口成为`MathTools`模块的类型：

```OCaml
    module MathTools: MathToolsInterface = {
      ···
    };
```

其次，我们也可以内联接口：

```OCaml
    module MathTools: {
      let times: (int, int) => int;
      let square: (int) => int;
    } = {
      ···
    };
```

#### 抽象类型：隐藏内部结构

你可以使用接口来隐藏类型的详细信息。让我们从`Log.re`模块开始，将一个字符串“放入”日志。它通过字符串实现日志，并通过直接使用字符串来完全公开这个实现细节：

```OCaml
    /* Log.re */

    let make = () => "";
    let logStr = (str: string, log: string) => log ++ str ++ "\n";

    let print = (log: string) => print_string(log);
```

从这段代码中，`make()`和`logStr()`返回的日志实际上是不清楚的。

这就是使用`Log`的方式，请注意在这种情况下管道（|>）使用是多么的便利：

```OCaml
    /* LogMain.re */

    let () = Log.make()
      |> Log.logStr("Hello")
      |> Log.logStr("everyone")
      |> Log.print;

    /* Output:
    Hello
    everyone
    */
```

改进`Log`的第一步是引入日志的类型。借用 OCaml 公约，使用名字`t`作为这个模块支持的主类型，例如：`Bytes.t`

```OCaml
    /* Log.re */

    type t = string; /* A */

    let make = (): t => "";
    let logStr = (str: string, log: t): t => log ++ str ++ "\n";

    let print = (log: t) => print_string(log);
```

在 A 行中，我们定义了`t`作为字符串的简单别名，由于可以有一个简单的开始，并且稍后可以添加更过的功能，所以别名非常方便。但是，别名迫使我们去注解`make()`和`logStr()`（否则将返回类型字符串）的结果。

完整的接口文件如下所示。

```OCaml
    /* Log.rei */

    type t = string; /* A */
    let make: (unit) => t;
    let logStr: (string, t) => t;
    let print: (t) => unit;
```

我们可以使用以下代码替换 A 行，并且`t`变成了抽象的——它的细节被隐藏了。这意味着我们可以很容易的在之后改变我们的想法，例如，通过一个数组来实现它。

```OCaml
    type t;
```

方便的是，我们不必改变`LogMain.re`，它仍然适用于新的模块。

### 从模块导入值

有几种可以从模块导入值的方法。

#### 通过模块名限制条件导入

我们已经看到，如果模块名是一个有限定的名字，可以自动导入由模块导出的值。例如，如下代码通过`Log`模块导入了`make`，`logStr`和`print`：

```OCaml
    let () = Log.make()
      |> Log.logStr("Hello")
      |> Log.logStr("everyone")
      |> Log.print;
```

#### 开放全局的模块

如果你开放`Log`模块为“全局”（在当前模块的范围内），则可以省略限定符`Log.`：

```OCaml
    open Log;

    let () = make()
      |> logStr("Hello")
      |> logStr("everyone")
      |> print;
```

为了避免命名冲突，该操作不经常使用。大多数模块，例如`List`，都是通过限定条件来使用：`List.length()`，`List.map()`等。

全局开放可以用于使用不同实现方式的标准模块。例如，`Foo`模块有一个叫做`List`的子模块，如果开放`Foo`，则标准`List`模块将被覆盖。

#### 开放本地模块

我们可以通过开放本地`Log`模块来使命名冲突风险最小化，同时仍然获得开放模块的便利性。可以通过给小括号语法加上`Log.`前缀来实现（即，我们限定了这个表达式），如下：

```OCaml
    let () = Log.(
      make()
        |> logStr("Hello")
        |> logStr("everyone")
        |> print
    );
```

##### 重定义运算符

很方便的是，ReasonML 中运算符也是函数，这使我们可以临时的覆盖内置运算符。比如，我们可能不喜欢带点的运算符来计算浮点数：

```OCaml
    let dist = (x, y) =>
      sqrt((x *. x) +. (y *. y));
```

然后我们可以通过`FloatOps`模块来重写一个更好的整型运算符：

```OCaml
module FloatOps = {
  let (+) = (+.);
  let (*) = (*.);
};
let dist = (x, y) =>
  FloatOps.(
    sqrt((x * x) + (y * y))
  );
```

你是否应该在生产代码这么做还是有争议的。

#### 包含模块

导入模块的另外一种方式是包含它，然后这个模块的所有导出都将添加到当前模块出口。这点类似于面向对象编程中的继承。

下例中，`LogWithDate`是`Log`模块的一个扩展，除了`Log`的所有函数外，它有一个新的函数`logStrWithDate()`。

```OCaml
/* LogWithDateMain.re */

module LogWithDate = {
  include Log;
  let logStrWithDate = (str: string, log: t) => {
    let dateStr = Js.Date.toISOString(Js.Date.make());
    logStr("[" ++ dateStr ++ "] " ++ str, log);
  };
};
let () = LogWithDate.(
  make()
    |> logStrWithDate("Hello")
    |> logStrWithDate("everyone")
    |> print
);
```

[`Js.Date`][bucklescript_js_date]来自于 BuckleScript 的标准函数库，此处不做详细解释。

你可以 include 你想要的多个模块，而不仅仅是一个。

#### 包含接口

接口也可以如下被包含（`InterfaceB`是`InterfaceA`的扩展）：

```OCaml
module type InterfaceA = {
  ···
};
module type InterfaceB = {
  include InterfaceA;
  ···
}
```

类似于模块，可以包括多个接口。

让我们为`LogWithDate`模块创建一个接口。可是，由于没有`Log`接口，所以我们不能通过这个模块的名字来包含`Log`。然而，我们可以通过模块间接引用它（A 行）：

```OCaml
module type LogWithDateInterface = {
  include (module type of Log); /* A */
  let logStrWithDate: (t, t) => t;
};
module LogWithDate: LogWithDateInterface = {
  include Log;
  ···
};
```

#### 重命名导入

你不能真正的重命名导入，但是你可以起一个别名。

如下给一个模块起别名：

```OCaml
module L = List;
```

如下在模块内部起别名：

```OCaml
let m = List.map;
```

### 命名空间模块

在大型项目中，ReasonML 识别模块名的方式可能会有问题。由于它具有单个全局命名空间，所以很容易有命名冲突。比如，不同文件夹中的两个模块都叫做`Util`。

一种技术方案是使用*命名空间模块(namespace modules)*，例如下面的项目结构：

    proj/
      foo/
        NamespaceA.re
        NamespaceA_Misc.re
        NamespaceA_Util.re
      bar/
        baz/
          NamespaceB.re
          NamespaceB_Extra.re
          NamespaceB_Tools.re
          NamespaceB_Util.re

这个项目中有两个`Util`模块，他们的唯一区别是他们的名字分别用`NamespaceA_`和`NamespaceB_`作为前缀：

    proj/foo/NamespaceA_Util.re
    proj/bar/baz/NamespaceB_Util.re

为了是命名不是显得很笨拙，每个命名空间都有一个*命名空间模块*，这第一个文件看起如下：

```OCaml
/* NamespaceA.re */
module Misc = NamespaceA_Misc;
module Util = NamespaceA_Util;
```

`NamespaceA`如下方式被使用：

```OCaml
/* Program.re */

open NamespaceA;

let x = Util.func();
```

全局开放`Util`可以让我们使用的时候不用带前缀。

这个技巧的使用还有两个用例：

* 你可以用它来覆盖模块，甚至覆盖标准库中的模块。例如，`NamespaceA.re`中包含一个自定义的`List`实现，就可以在`Program.re`内部使用的时候，覆盖掉内建的`List`模块：

```OCaml
module List = NamespaceA_List;
```

* 你可以创建嵌套模块同时将子模块保存在单独的文件里。例如，除了开放`NamespaceA`，由于`Util`是嵌套在`NamespaceA`里的，所以你也可以通过`NamespaceA.Util`访问`Util`。当然，也可以使用`NamespaceA_Util`，但是令人沮丧的是，这个是一个实现细节。

这个后一种技术被`BuckleScript`用于`Js.Date`，`Js.Promise`等，在`js.ml`文件中（使用的 OCaml 语法）：

```OCaml
···
module Date = Js_date
···
module Promise = Js_promise
···
module Console = Js_console
```

#### OCaml 中命名空间模块

命名空间模块在 OCaml 中被广泛使用，在 Jane Street。他们叫做*打包模块*，但是由于它和 npm 包规则不冲突，所以我更喜欢叫做*命名空间模块*。

本章节来源：“[Better namespaces through module aliases][module_from]”，Yaron Minsky 在 Jane Street 技术博客发表。

### 标准函数库探索

ReasonML 标准库有两个重要的警告：

* 它目前正在进行中。

* 模块内的命名方式将要从下划线式（`foo_bar`和`Foo_bar`）改为驼峰式（`fooBar`和`FooBar`）。

* 目前，还有许多功能缺失。

#### API 文档

ReasonML 标准库被拆分为：ReasonML 的绝大多数核心 API 都可以在原生和 JavaScript（通过 BuckleScript）上工作。如果你想编译成 JavaScript，你需要在两种情况下使用 BuckleScript 的 API：

* ReasonML 里完全缺失了功能性 API。例子包括了对日期的支持，你需要通过 BuckleScript 的[`Js.Date`][js_date]得到。

* ReasonML API 设计目的就不支持的需要通过 BuckleScript。例子包含了`Str`模块（由于 JavaScript 的字符串和 ReasonML 原生的不同）和`Unix`（带有原生 API）。

以下是两种 API 文档：

* [ReasonML API 文档][reasonml_api_doc]
* [BuckleScript API 文档][bucklescript_api_doc]

#### *Pervasives*模块（处处可以随时访问的模块）

[Pervasives 模块][pervasives]包含了核心标准库并且总是在每一个模块都自动处于打开状态（opened）。它包含的功能诸如`==`，`+`，`|>`操作符和`print_string()`、`string_of_int()`函数。

如果一些功能在某些模块内部被重写覆盖了，你仍可以通过例如`Pervasives.(+)`来显示访问。

如果在项目中存在一个名为`Pervasives.re`的文件，它重写了内建模块并且将代替其开放（opened）。

#### 带标记参数的标准函数

下面的模块存在于两个版本中：一个旧版本，其中函数仅有位置参数；一个更新版本，其函数也有标记参数。

* `Array`, `ArrayLabels`
* `Bytes`, `BytesLabels`
* `List`, `ListLabels`
* `String`, `StringLabels`

考虑以下例子：

```OCaml
List.map: ('a => 'b, list('a)) => list('b)
ListLabels.map: (~f: 'a => 'b, list('a)) => list('b)
```

另外两个提供标记函数的模块：

* [StdLabels 模块][std_labels_module]具有`Array`，`Bytes`，`List`，`String`，`ArrayLabels`的别名等子模块。在你自己的模块里，默认情况下，可以打开`StdLabels`模块来获取`List`的标记版本。

* [MoreLabels 模块][more_labels_module]有三个具有标记函数的子模块：`Hashtbl`，`Map`和`Set`。

### 安装库

目前，JavaScript 是 ReasonML 的首选平台，因此，安装库的首选方法是通过 npm。如下方式进行操作。作为一个例子，假设我们想要安装`Jest`的 BuckleScript 绑定（包含 Jest 本身）。这个相关的 npm 包名叫做`bs-jest`。

首先，我们需要安装这个 npm 包，在你的`package.json`包含如下内容：

```json
{
  "dependencies": {
    "bs-jest": "^0.1.5"
  },
  ···
}
```

然后，你需要添加这个包到`bsconfig.json`：

```json
{
  "bs-dependencies": [
    "bs-jest"
  ],
  ···
}
```

之后，我们就可以像`Jest.describe()`等方式使用 Jest 模块了。

更多关于安装库的信息：

* BuckleScript 构建系统在 BuckleScript 手册中[构建系统支持][bucklescript_build_sys_support]这一章节详细说明。

* ReasonML 文档中详细说明了[怎么在 npm 中发现 ReasonML 库][reasonml_doc_find_npm_lib]。

  * 有用的 npm 关键字包含：reason，reasonml，bucklescript。

[阅读原文][origin_url]
