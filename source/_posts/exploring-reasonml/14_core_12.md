---
title: 探索ReasonML_14 - 函子（高级）
date: 2018-09-08 16:50:25
tags: [ReasonML]
category: 'reasonml'
---

[origin_url]: http://reasonmlhub.com/exploring-reasonml/ch_functors.html
[github_factor_demo]: https://github.com/rauschma/reasonml-demo-functors
[reasonml_set_api]: https://reasonml.github.io/api/Set.html

直观的，一个*函子（functor）*是一个函数，它将一个或多个模块当做输入，并且返回一个新的模块。本章节讲解函子若何运行并且它们有什么作用。

事例代码在 GitHub[仓库 reasonml-demo-functors][github_factor_demo]。

注意：函子是一个高级话题，可能刚开始你不知道怎么写。我会解释一些其他地方需要使用它们的相关基础知识。

### 什么是函子？

*函子（functor）*是范畴论中的一个术语，是指范畴之间的映射。在 ReasonML 中，有两种理解函子的方法：

- 是一个以模块为参数，并且返回值也是模块的函数。

- 通过模块（参数）参数化（配置）的一个模块（返回值）。

只能将函子定义为子模块（它不能是文件中的顶级元素）。但是这不是问题，因为通常希望在模块内传递一个函子，它通常挨着接口的参数（可选）和返回值。

函子的语法如下：

```OCaml
module «F» = («ParamM1»: «ParamI1», ···): «ResultI» => {
«body of functor»
};
```

函子`F`有一个或多个模块`ParamM1`等作为参数，这些模块必须通过接口`ParamI1`等键入。返回值类型`ResultI`（另一个接口）是可选的。

函子主体与一般模块具有相同的语法，但是它可以引用参数及其其内容。

### 第一个函子

#### 定义函子`Repetition.Make`

作为第一个示例，定义一个函子`Repetition.Make`，该函子导出一个重复字符串参数的函数`repeat()`，函子参数配置了字符串重复的次数。

定义函子之前，我们需要为其参数定义一个接口`Repetition.Count`。

```OCaml
/* Repetition.re */

module type Count = {
  let count: int;
};
```

函子如下所示：

```OCaml
/* Repetition.re */

module Make = (RC: Count) => {
  let rec repeat = (~times=RC.count, str: string) =>
    if (times <= 0) {
      "";
    } else {
      str ++ repeat(~times=times-1, str);
    };
};
```

`Make`是一个函子：它的参数是一个`RC`模块，它的返回值（在大括号中）也是一个模块。

`repeat()`是一个比较无聊的 ReasonML 函数，它唯一的新方面是来自于模块`RC`默认值`~times`。

#### 用于重复`Repetition.Make`返回值的接口

`Repetition.Make`目前不定义其结果类型，这意味着 ReasonML 推断出了其类型。如果想要更多控制，可以为返回值定义一个接口`S`：

```OCaml
/* Repetition.re */

module type S = {
  let repeat: (~times: int=?, string) => string;
};
```

之后，我们只需要在定义中加入`S`：

    module Make = (RC: Count): S => ···

#### 使用`Repetition.Make`

接下来，在一个单独的文件里使用`Repetition.Make`。首先，我们为函子的参数定义一个模块：

```OCaml
/* RepetitionMain.re */

module CountThree: Repetition.Count = {
  let count=3;
};
```

不需要`Repetition.Count`类型，只要`CountThree`与该接口具有完全相同的数据结构。但是它可以使人立即明白`CountThree`的目的是什么。

现在准备使用`Repetition.Make`函子创建一个模块：

```OCaml
/* RepetitionMain.re */

module RepetitionThree = Repetition.Make(CountThree);
```

如下代码调用了`RepetitionThree.repeat`，并且达到了预期效果：

```OCaml
/* RepetitionMain.re */

let () = print_string(RepetitionThree.repeat("abc\n"));

/* Output:
abc
abc
abc
*/
```

没有必要单独定义`CountThree`模块，也可以把它内联起来：

```OCaml
module RepetitionThree = Repetition.Make({
  let count=3;
});
```

#### `Repetition`数据结构

构造`Repetition`模块是使用函子的常见模式，他有如下几个部分：

- `Make：`函子。
- `Make`的一个或多个参数接口（实例中的`count`）。
- `S`：`Make`返回值的接口。

也就是说，`Repetition`打包了函子`Make`所需要的一切。

### 数据结构的函子

函子的一个常见用例是用来实现数据结构：

- 函子的参数数据结构必须被明确说明，由于参数本质是模块，所以不仅仅可以明确指出元素的类型，也可以辅助函数管理数据结构。
- 函子的返回值是一个为特定元素量身定制的模块。

例如，[数据结构`Set`][reasonml_set_api]（我们稍后将详细查看）必须能够比较它的元素。因此，set 的函子有一个如下接口的参数：

```OCaml
module type OrderedType = {
  type t;
  let compare: (t, t) => int;
};
```

`OrderedType.t`是 set 元素的类型，`OrderedType.compare`被用于比较这些元素。`OrderedType.t`与多态类型`list('a)`中的变量`'a`类似。

#### `PrintablePair1`：可打印组件对的第一个版本

来实现一个非常简单的数据结构：可以*打印*（转换为字符串）任意组件对。为了打印组件对，必须能够打印他们的组件。这也是为什么组件必须通过以下接口来指定的原因。

```OCaml
/* PrintablePair1.re */

module type PrintableType = {
  type t;
  let print: t => string;
};
```

再次使用`Make`作为函子名称，改函子用于生成实际数据结构模块：

```OCaml
/* PrintablePair1.re */

module Make = (Fst: PrintableType, Snd: PrintableType) => {
  type t = (Fst.t, Snd.t);
  let make = (f: Fst.t, s: Snd.t) => (f, s);
  let print = ((f, s): t) =>
    "(" ++ Fst.print(f) ++ ", " ++ Snd.print(s) ++ ")";
};
```

这个函子有两个参数：`Fst`是可打印对的第一个组件，`Snd`是第二个组件。

由`PrintablePair1.Make`返回的模块有以下几部分：

- `t`是该函子支持的数据结构类型。请注意是如何引用函子的参数`Fst`和`Snd`的。
- `make`是创建类型`t`值的函数。
- `print`是实现打印组件对的函数，他将其转换成了字符串。

##### 使用这个函子

使用`PrintablePair1.Make`创建一个可打印对，该对的第一个组件是字符串，第二个组件是整形。

首先，我们需要定义函子的参数：

```OCaml
/* PrintablePair1Main.re */

module PrintableString = {
  type t=string;
  let print = (s: t) => s;
};
module PrintableInt = {
  type t=int;
  let print = (i: t) => string_of_int(i);
};
```

接下来，使用该函子创建模块`PrintableSI`：

```OCaml
/* PrintablePair1Main.re */

module PrintableSI = PrintablePair1.Make(PrintableString, PrintableInt);
```

最后，我们创建并打印组件对：

```OCaml
/* PrintablePair1Main.re */

let () = PrintableSI.({
  let pair = make("Jane", 53);
  let str = print(pair);
  print_string(str);
});
```

#### 改进封装

当前实现有一个缺陷，我们可以在不使用`PrintableSI.make()`的情况下创建类型为`t`的元素：

```OCaml
let pair = ("Jane", 53);
let str = print(pair);
```

为了防止这一点，我们需要通过接口来创建抽象类型`Make.t`：

```OCaml
/* PrintablePair1Main.re */

module type S = (Fst: PrintableType, Snd: PrintableType) => {
  type t;
  let make: (Fst.t, Snd.t) => t;
  let print: (t) => string;
};
```

以下方式定义`Make`使其有`S`的特性：

    module Make: S = ···

注意`S`是一个完整函子的类型。

#### PrintablePair2：一个适用于返回值的接口

更常见的是只定义函子的返回值（而不是完整的函子，就像之前那样）。这样，就可以复用接口进行其他用途了。对于正在讨论的这个示例，这样的接口如下所示：

```OCaml
/* PrintablePair2.re */

module type S = {
  type fst;
  type snd;
  type t;
  let make: (fst, snd) => t;
  let print: (t) => string;
};
```

现在我们不能不能再参考函子的参数`Fst`和`Snd`了，因此我们需要介绍两种新的类型`fst`和`snd`，我们需要用来定义`make()`类型，该函数以前有以下类型：

    let make: (Fst.t, Snd.t) => t;

那么，我们如何将`fst`和`snd`与`Fst.t`和`Snd.t`连接起来呢？可以通过所谓的*共享约束（sharing constraints）*，等价改变接口。它们被如下使用：

```OCaml
/* PrintablePair2.re */

module Make = (Fst: PrintableType, Snd: PrintableType)
: (S with type fst = Fst.t and type snd = Snd.t) => {
  type fst = Fst.t;
  type snd = Snd.t;
  type t = (fst, snd);
  let make = (f: fst, s: snd) => (f, s);
  let print = ((f, s): t) =>
    "(" ++ Fst.print(f) ++ ", " ++ Snd.print(s) ++ ")";
};
```

以下两个等式是共享约束：

    S with type fst = Fst.t and type snd = Snd.t

`S with`暗示了接口约束的改变，事实确实如此。`Make`返回值的接口如下：

```Ocaml
{
  type fst = Fst.t;
  type snd = Snd.t;
  type t;
  let make: (fst, snd) => t;
  let print: (t) => string;
}
```

还有一件事情我们可以改进：`fst`和`snd`是累赘的，如果返回值直接引用`Fst.t`和`Snd.t`（就像我们拥有用于完整函子的接口时那样）会更好。这是通过破坏性替换来完成的。

#### PrintablePair3：破坏性替换

*破坏性替换（destructive substitutions）*很像共享约束，但是：

- 共享约束`S with type t = u`为`S.t`提供了更多信息。
- 破坏性替换`with type t := u`使用`u`替换了`S`中所有的`t`。

如果我们使用了破坏性替换，`Make`看起来如下所示：

```OCaml
module Make = (Fst: PrintableType, Snd: PrintableType)
: (S with type fst := Fst.t and type snd := Snd.t) => {
  type t = (Fst.t, Snd.t);
  let make = (fst: Fst.t, snd: Snd.t) => (fst, snd);
  let print = ((fst, snd): t) =>
    "(" ++ Fst.print(fst) ++ ", " ++ Snd.print(snd) ++ ")";
};
```

破坏性替换将`fst`和`snd`从`S`中移除，因此，不再需要在`Make`函数体中定义他们，并且还总可以直接引用`Fst.t`和`Snd.t`。由于破坏性替换，`Make`内部定义了这个接口所需要的匹配。现在`Make`的返回值签名如下：

```OCaml
{
  type t;
  let make: (Fst.t, Snd.t) => t;
  let print: (t) => string;
}
```

#### 示例：使用函子 Set.Make

值集的标准模块`Set`遵循已经解释过的如下约定：

- `Set.Make`是用于生成实际处理集合实际模块的函子。
- `Set.OrderedType`是`Make`参数的接口：

```OCaml
module type OrderedType = {
  type t;
  let compare: (t, t) => int;
};
```

- `Set.S`是`Make`返回值的接口。

这是如何创建并使用一个字符串 set 模块：

```OCaml
module StringSet = Set.Make({
  type t = string;
  let compare = Pervasives.compare;
});

let set1 = StringSet.(empty |> add("a") |> add("b") |> add("c"));
let set2 = StringSet.of_list(["b", "c", "d"]);

/* StringSet.elements(s) converts set s to list */
StringSet.(elements(diff(set1, set2)));
  /* list(string) = ["a"] */
```

为方便起见，ReasonML 的标准函数库附带一个模块，由于`String`有`String.t`和`String.compare`，所以该模块以`String`作为`Set.Make`的参数。因此，我们也可以写作：

    module StringSet = Set.Make(String);

### 使用函子扩展模块

函子也可以用来扩展现有模块的功能，这种方式使用起来，类似于多重继承和混合（抽象子类）。

作为一个示例，让我们扩展一个现有模块，该模块只能向其数据结构添加单个元素，并且具有添加给定数据结构所有元素的方法。`AddAll`是如下所实现的函子：

```OCaml
module AddAll = (A: AddSingle) => {
  let addAll = (~from: A.t, into: A.t) => {
    A.fold((x, result) => A.add(x, result), from, into);
  };
};
```

`addAll()`函数使用`fold()`遍历`~from`元素同时进行逐个添加。`result`总是绑定到已经计算过的结果（首先是`into`，然后添加到第一个`x`到`into`作为结果，等等）。

在这种情况下，我们让 ReasonML 推断`AddAll`结果的类型，并且不提供他接口。如果我们这样做，它将要有名字 S 和抽象类型`t`（对于`addAll`的参数和返回值）。将要被这样使用：

```OCaml
module AddAll = (A: AddSingle)
: (S with type t := A.t) => {
  ···
};
```

从源码上看，我们可以推断出`addAll`需要并且聚合了接口`AddSingle`：

```OCaml
module type AddSingle = {
  type elt;
  type t; /* type of data structure */

  let empty: t;
  let fold: ((elt, 'r) => 'r, t, 'r) => 'r;
  let add: (elt, t) => t;
};
```

#### 字符串集合的 AddAll（`AddAll` for sets of strings）

我们采用已经预先定义的`StringSet`来创建`StringSetPlus`：

```OCaml
module StringSetPlus = {
  include StringSet;
  include AddAll(StringSet);
};
```

新模块`StringSetPlus`包含`StringSet`模块和应用`AddAll`函子到`StringSet`模块的结果。如你所愿，我们正在做模块间的多重继承。

如下是`StringSetPlus`的使用：

```OCaml
let s1 = StringSetPlus.of_list(["a", "b", "c"]);
let s2 = StringSetPlus.of_list(["b", "c", "d"]);
StringSetPlus.(elements(addAll(~from=s2, s1)));
  /* list(string) = ["a", "b", "c", "d"] */
```

#### 我们能够简化`AddAll`吗？

目前，我们需要将`StringSet`模块和扩展`AddAll(StringSet)`结合起来创建`StringSetPlus`：

```OCaml
module StringSetPlus = {
  include StringSet; /* base */
  include AddAll(StringSet); /* extension */
};
```

我们是否可以如下创建些什么东西呢？

    module StringSetPlus = AddAll(StringSet);

我们不这么做有两个原因。

首先，想保持`AddAll`参数和`StringSetPlus`的基础部分的分离。当我们使用`AddAll`列表时，我们需要分离。

第二， 没有办法实现`AddAll`，因此扩展了它的参数。理论上，情况会是这样：

    module AddAll = (A: AddSingle) => {
      include A;
      ···
    };

在实际使用中，其内仅包含`A`的接口`AddSingle`，是通常不够的。

#### 数据结构：多态数据类型 vs. 函子

ReasonML 标准库提供了两种数据结构：

- `list`和其他多态类型数据结构，其元素是通过类型变量指定。

- `set`和其他通过函子实现的数据结构，其元素类型是通过模块指定的。

#### 字符串列表的`AddAll`（`AddAll` for lists of strings）

哎，`AddAll`最适合通过函子实现的数据结构，如果想要将他用于 list，必须把类型变量`list('a)`绑定到一个具体的类型（当前情况是`string`）。这导致了`AddAll`的如下参数：

```OCaml
module AddSingleStringList
: AddSingle with type t = list(string) = {
  type elt = string;
  type t = list(elt);
  let empty = [];
  let fold = List.fold_right;
  let add = (x: elt, coll: t) => [x, ...coll];
};
```

【这是我能想出的最简单的解决方案——欢迎提出改进建议。】

之后，这就是如何创建和使用支持所有 list 加操作的`addAll()`模块：

```OCaml
module StringListPlus = {
  include List;
  include AddAll(AddSingleStringList);
};

StringListPlus.addAll(~from=["a", "b"], ["c", "d"]);
  /* list(string) = ["a", "b", "c", "d"] */
```

### 资料

- Caml 手册[“The module system”](http://caml.inria.fr/pub/docs/manual-ocaml/moduleexamples.html)章节
- “Real World OCaml”的[“Functors”](https://realworldocaml.org/v1/en/html/functors.html)章节
- [“Modules”](https://ocaml.org/learn/tutorials/modules.html)（OCaml.org 教程）

[阅读原文][origin_url]
