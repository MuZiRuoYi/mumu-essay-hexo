---
title: 探索ReasonML_07 - 函数
date: 2018-04-10 18:23:16 -0700
tags: [ReasonML]
category: 'reasonml'
---

[origin_url]: http://reasonmlhub.com/exploring-reasonml/ch_functions.html
[lists_chapter]: http://reasonmlhub.com/exploring-reasonml/ch_lists.html
[variant_chapter]: http://reasonmlhub.com/exploring-reasonml/ch_variants.html
[ocaml_expression]: https://caml.inria.fr/pub/docs/manual-ocaml/expr.html
[ocaml_infixsymbol]: https://caml.inria.fr/pub/docs/manual-ocaml/lex.html#infix-symbol
[ocaml_label]: (https://caml.inria.fr/pub/docs/manual-ocaml/lablexamples.html#sec45

### 定义函数

一个匿名函数如下所示：

```OCaml
    (x) => x + 1;
```

这个函数有一个参数`x`，和函数体`x + 1`.

你可以把函数绑定到一个变量来赋予它一个函数名：

```OCaml
    let plus1 = (x) => x + 1;
```

如下方式来调用函数`plus1`：

```OCaml
    # plus1(5);
    - : int = 6
```

#### 以函数作为参数（高阶函数）

函数也可以作为参数使用。我们暂时使用[_lists_][lists_chapter]来证明这个问题。*Lists*大致上是单链表，类似于不可变数组。

list 的一个方法`List.map(func, list)`，其接受一个`list`，调用`func`处理`list`的每个元素，并且返回一个新的 list 做为返回值。例如：

```OCaml
    # List.map((x) => x + 1, [12, 5, 8, 4]);
    - : list(int) = [13, 6, 9, 5]
```

以函数为参数或返回值的函数称为*高阶函数*，此外的则被称为*一阶函数*。`List.map()`是一个高阶函数，`plus1()`是一个一阶函数。

#### 代码块作为函数体

一个函数体是一个表达式。考虑到定义域块也是表达式，以下两个定义是等价的：

```OCaml
    let plus1 = (x) => x + 1;

    let plus1 = (x) => {
      x + 1
    };
```

### 没有括号的单参数

如果一个函数只有一个参数，并且参数是通过标识符定义的，那么可以省略括号：

```OCaml
    let plus1 = x => x + 1;
```

### 使用 let rec 递归绑定

通常，只能引用已经存在的 let 绑定值，这意味着不能定义互递归和自递归函数。

#### 定义互递归函数

让我们先看一下互递归函数。下面两个函数`event`和`odd`是互递归函数（这是一个示例，而不是实际上去实现这些函数）。你必须使用特殊的`let rec`来定义他们：

```OCaml
    let rec even = (x) =>
      if (x <= 0) {
        true;
      } else {
        odd(x - 1);
      }
    and odd = (x) =>
      if (x <= 0) {
        false;
      } else {
        even(x - 1);
      };
```

请注意`and`是怎么连接多个`let rec`让他们互相知道对方：在`and`之前没有分号，结尾的分号说明`let rec`的结束。

让我们使用这两个函数：

```OCaml
    # even(11);
    - : bool = false
    # even(2);
    - : bool = true
    # odd(11);
    - : bool = true
    # odd(2);
    - : bool = false
```

#### 定义自递归函数

由于调用递归的时候，绑定还不存在，所以还得使用 let rec 来定义函数，进行自身递归。例如：

```OCaml
    let rec factorial = (x) =>
      if (x <= 2) {
        x
      } else {
        x * factorial(x - 1)
      };

    factorial(3); /* 6 */
    factorial(4); /* 24 */
```

### 术语：元数（arity）

元数对于函数来说，是指它有多少个参数。对于`factorial()`的元数是一。以下的形容词描述了函数从 0 元到 3 元：

* 一个无参（nullary）函数是 0 元（arity 0）函数
* 一个一元（unary）的函数是 1（arity 1） 元函数
* 一个二元（binary）的函数是 2（arity 2）元函数
* 一个三元（ternary）的函数是 3（arity 3）元函数

超过 3 元，我们叫做 4 元（4-ary）函数，五元（4-ary）函数等。有的函数的元数是可变的，被称作可变元数函数（variadic functions），在其他语言中也有被称作*可变参*函数（_varargs_）。

### 函数的类型

函数是我们第一次接触复杂的数据类型：通过其他类型组合构建出来的类型。让我们在 rtop 上确定一个函数的类型。

#### 一阶函数类型

首先，看以下函数`add()`：

```OCaml
    # let add = (x, y) => x + y;
    let add: (int, int) => int = <fun>;
```

因此`add()`的数据类型是：

```OCaml
    (int, int) => int
```

这个箭头表示`add`是一个函数，它的参数是两个整型（int），其返回值是一个整型（int）。

这个标记`(int, int) => int`也被称作`add`的（类型）签名，它描述了它的输入输出的类型。

#### 高阶函数类型

其次，让我们看一个高阶函数`callFunc()`：

```OCaml
    # let callFunc = (f) => f(1) + f(2);
    let callFunc: ((int) => int) => int = <fun>;
```

看以下`callFunc()`怎么被使用：

```OCaml
    # callFunc(x => x);
    - : int = 3
    # callFunc(x => 2 * x);
    - : int = 6
```

#### 类型注解（type annotations）和类型推论（type inference）

在 ReasonML 中，类型注解是可选择的，但是它们可以提交类型检查的准确性。最极端的做法是所有地方都做注解：

```OCaml
    let add = (x: int, y: int): int => x + y;
```

我们为函数的两个参数`x`和`y`，及其函数返回值（箭头之前`: int`）进行了注解。

可以省略返回值的类型注解，ReasonML 会对函数返回值进行类型推断（根据参数的类型进行推导）：

```OCaml
    # let add = (x: int, y: int) => x + y;
    let add: (int, int) => int = <fun>;
```

但是，*类型推断*比那个更复杂，它不仅能自顶向下（top-down），还能根据参数和`int`加法运算符（`+`，没有重载，只能运算整型加法）进行推断。

```OCaml
    # let add = (x, y) => x + y;
    let add: (int, int) => int = <fun>;
```

如果你愿意，还可以对部分参数进行注解：

```OCaml
    let add = (x, y: int) => x + y;
```

#### 类型注解：最佳实践

我更喜欢注解所有参数，让 ReasonML 推断返回值类型的编码风格。除了优化类型检查，注解也是很好的文档。

### 没有参数就没有函数

ReasonML 没有 0 元函数，但是你可以在没有注意的情况下使用它。

回想一下，这里的`()`大致相当于 C 类语言里的`null`，它是 unit 类型的唯一元素。当调用函数是，省略参数和仅传一个参数 unit 是相同的。也就是说，下面两个表达式是等价的：

```OCaml
    func()
    func(())
```

下面例子说明了一个现象：当你调用一元函数时没有传参，rtop 将要强调表达式参数`()`错误，而不是提供参数数目不够（有时也不适用——稍后详解）。

```OCaml
    # let id = (x: int) => x;
    let id: (int) => int = <fun>;
    # id();
    Error: This expression has type unit but
    an expression was expected of type int
```

如果你定义了一个函数没有参数，ReasonML 将会添加一个 unit 类型的参数：

```OCaml
    # let f = () => 123;
    let f: (unit) => int = <fun>;
```

#### 为什么没有 0 元函数？

为什么 ReasonML 没有 0 元函数？这是由于 RationalML 始终执行[部分应用](#部分应用（partial-application）)（稍后详细解释）：如果你不给一个函数提供参数，你将会从剩余参数中得到一个新的函数。因此，如果实际上根本没有提供任何参数，那么`func()`将与`func`相同，而且实际上也不会调用`func`。

### 解构函数参数

解构可以用于任何变量绑定到值。也就是说，它在参数定义中也起作用。让我们看一个添加元组组件函数：

```OCaml
    let addComponents = ((x, y)) => x + y;
    let tuple = (3, 4);
    addComponents(tuple); /* 7 */
```

`x, y`外边的小括号表明`addComponents`是一个以元组为参数的一元函数，这个元组的组件是`x`和`y`，而不是一个以`x`和`y`为参数的二元函数。它的类型是：

```OCaml
    addComponents: ((int, int)) => int
```

当涉及到类型注解的时候，你也可以对元组组件进行注解：

```OCaml
    # let addComponents = ((x: int, y: int)) => x + y;
    let addComponents: ((int, int)) => int = <fun>;
```

或者你也可以对整个元组参数进行注解：

```OCaml
    # let addComponents = ((x, y): (int, int)) => x + y;
    let addComponents: ((int, int)) => int = <fun>;
```

### 标记参数

迄今为止，我们仅使用了*位置参数（positional parameters）*：函数调用是实参的位置决定了它所绑定的形参。

但是 ReasonML 也支持*标记参数（labeled parameters）*。在这里，标记用于将实参和型参相关联。

作为示例，我们看一下`add`的另一版本，使用标记参数的版本：

```OCaml
    let add = (~x, ~y) => x + y;
    add(~x=7, ~y=9); /* 16 */
```

在这个函数的定义里，我们将标记`~x`和参数`x`使用了相同的名字。你也可以使用不通的名字，比如标记`~x`和参数`op1`：

```OCaml
    /* Inferred types */
    let add = (~x as op1, ~y as op2) =>
      op1 + op2;

    /* Specified types */
    let add = (~x as op1: int, ~y as op2: int) =>
      op1 + op2;
```

在调用函数是，你可以把`~x = x`简写为`~x`：

```OCaml
    let x = 7;
    let y = 9;
    add(~x, ~y);
```

标记的一个很好特性是，使用时你可以把标记参数按任何顺序使用：

```OCaml
    # add(~x=3, ~y=4);
    - : int = 7
    # add(~y=4, ~x=3);
    - : int = 7
```

#### 函数类型与标记参数的兼容性

这里关于标记参数任意顺序的问题，有一个令人遗憾的警告：函数类型只有在标记以相同的顺序被引用时才是兼容的。

思考以下三个函数：

```OCaml
    let add = (~x, ~y) => x + y;
    let addxy = (add: ((~x: int, ~y: int) => int)) => add(5, 2);
    let addyx = (add: ((~y: int, ~x: int) => int)) => add(5, 2);
```

传入`add`，`addxy`如预期一样，正常运行：

```OCaml
    # addxy(add);
    - : int = 7
```

然而，由于标记顺序错误，我们在运行`addyx`时得到一个错误：

```OCaml
    # addyx(add);
    Error: This expression has type
    (~x: int, ~y: int) => int
    but an expression was expected of type
    (~y: int, ~x: int) => int
```

### 可选参数

在 ReasonML 里，仅标记参数是可选的。在下面的代码中，x 和 y 都是可选的：

```OCaml
    let add = (~x=?, ~y=?, ()) =>
      switch (x, y) {
      | (Some(x'), Some(y')) => x' + y'
      | (Some(x'), None) => x'
      | (None, Some(y')) => y'
      | (None, None) => 0
      };
```

让我们来看看相对复杂的代码是怎么做的。

为什么`()`作为最后一个参数？接下来的章节将会讲解。

`switch`表达式是什么？如果你声明一个可选参数，则它总有一个类型`option(t)`，其中`t`是指实际参顺序数值所具有的任何类型。`option`是一个*变体（variant）*（将要在[单独章节][variant_chapter]里讲解）。现在让我们有一个简单的概览，`option`的定义是：

```OCaml
    type option('a) = None | Some('a);
```

其用途如下：

* 省略`~x`和`x`，将与`None`绑定
* 为`~x`和`x`提供值`123`，将要与`Some(123)`绑定

换句话说，示例中`option`包装值，而`switch`表达式解包。

#### 使用可选参数，至少需要一个位置参数

为什么`add`函数最后有个`unit`参数（如果所愿的空参数）？

```OCaml
    let add = (~x=?, ~y=?, ()) =>
    ···
```

这么做的原因与[部分应用](#部分应用（partial-application）)有关，稍后将做详细讲解。简而言之，这有两个事情相冲突：

* 在[部分应用](#部分应用（partial-application）)中，如果省略参数，则创建一个函数来填充那些剩余的参数。

* 使用可选参数，如果省略参数，则应绑定到它们的默认值。

为了解决这一冲突，当遇到第一位置参数时，ReasonML 填充所有缺省值，在遇到第一个位置参数之前，它将一直等待缺省的可选参数。也就是说，你需要一个位置参数来触发该调用。以为`add()`没有一个位置参数，所以我们添加了一个为空的（位置参数）。

这个稍微有点奇怪的解决方案让你同时很好的得到了两个世界：[部分应用](#部分应用（partial-application）)和可选参数。

#### 可选参数类型注解

当你注解一个可选参数，他们必须有`option(...)`类型：

```OCaml
    let add = (~x: option(int)=?, ~y: option(int)=?, ()) =>
    ···
```

`add`的类型签名是：

```OCaml
    (~x: int=?, ~y: int=?, unit) => int
```

不幸的是，在这种情况下，定义与类型签名不同，但是它与默认值的参数相同（稍后再解释），其思想是隐藏掉如何处理可选参数的实现细节。

#### 参数默认值

处理缺失的参数可能是繁琐的：

```OCaml
    let add = (~x=?, ~y=?, ()) =>
      switch (x, y) {
      | (Some(x'), Some(y')) => x' + y'
      | (Some(x'), None) => x'
      | (None, Some(y')) => y'
      | (None, None) => 0
      };
```

在这种情况下，当`x`和`y`被省略的时候，我们希望他们都取`0`，ReasonML 有特殊的语法：

```OCaml
    let add = (~x=0, ~y=0, ()) => x + y;
```

#### 带参数默认值的类型注解

如果参数有默认值，类型注解更直观（没有`option()`）：

```OCaml
    let add = (~x: int=0, ~y: int=0, ()) =>
      x + y;
```

`add`的类型签名如下：

```OCaml
    (~x: int=?, ~y: int=?, unit) => int
```

#### 将选项值传递给可选参数（高级）

在函数内部，可选参数被接收为 option 类型（`None`或者`Some(x)`）的元素。目前为止，你只能通过提供参数或省略参数来传递这些值，但是，也有一种直接传递这些值的方式。在我们开始使用这个特性之前，先运行一下以下函数：

```OCaml
    let multiply = (~x=1, ~y=1, ()) => x * y;
```

`multiply`函数有两个可选参数，让我们先通过 option 元素提供参数`~x`并且省略参数`~y`：

```OCaml
    # multiply(~x = ?Some(14), ~y = ?None, ());
    - : int = 14
```

传递 option 值的语法是：

```OCaml
    ~label = ?expression
```

如果*expression*是一个名为*label*的变量，则可以进行缩写，以下两个句法是等价的：

```OCaml
    ~foo = ?foo
    ~foo?
```

那么用例意味着什么呢？这是一个将一个可选参数转发给另外一个函数的可选参数的函数，这样，它就可以依赖函数参数的默认值，而不必自己定义参数。

让我们看一个例子，下面的函数`square`有一个可选参数，这个可选参数被传递给了`multiply`的两个可选参数：

```OCaml
    let square = (~x=?, ()) => multiply(~x?, ~y=?x, ());
```

`square`不必指定参数的默认值，它可以使用`multiply`的默认值。

### 部分应用（partial application）

*部分应用*是一种使函数更加通用的机制：如果你在函数`f(...)`调用的结尾省略一个或多个参数，`f`返回了一个函数，这个函数将缺失的参数映射到`f`函数的最终结果[[注 1](#note_1)]。也就是说，将多步调用`f`进行传参，这第一步叫做*部分应用*或*部分调用*。

让我们看一下效果如何，首先创建一个带有两个参数的函数`add`，如下：

```OCaml
    # let add = (x, y) => x + y;
    let add: (int, int) => int = <fun>;
```

然后我们部分调用这个二元函数来创建一个一元函数`plus5`：

```OCaml
    # let plus5 = add(5);
    let plus5: (int) => int = <fun>;
```

我们仅为`add`提供了一个参数`x`，当我们调用`pluse5`时为`add`提供第二个参数`y`：

```OCaml
    # plus5(2);
    - : int = 7
```

#### 部分应用有什么用？

部分应用可以让你编写更加紧凑（简洁，compact）的代码。为了证明这一点，我们将使用一个由数字组成的*list*：

```OCaml
    # let numbers = [11, 2, 8];
    let numbers: list(int) = [11, 2, 8];
```

接下来，我们将使用标准库函数`List.map`。`List.map(func, myList)`中，`myList`序列中的每一个元素调用`func`，并且每次调用结果组成一个新的*list*作为其返回值。

我们使用`List.map`给`myList`的每个数字加`2`：

```OCaml
    # let plus2 = x => add(2, x);
    let plus2: (int) => int = <fun>;
    # List.map(plus2, numbers);
    - : list(int) = [13, 4, 10]
```

通过部分应用，我们可以使代码更紧凑（简洁，compact）：

```OCaml
    # List.map(add(2), numbers);
    - : list(int) = [13, 4, 10]
```

让我们更直接的对这两种方式进行对比：

```OCaml
    List.map(x => add(2, x), numbers)
    List.map(add(2), numbers)
```

哪个方式更好？这要取决于个人爱好了。第一种可以说是更加自我描述（self-descriptive）的，第二种更加的精炼简洁（concise）。

部分应用真的对于管道操作符（|>）组成函数的表现突出（稍后解释）。

#### 部分应用和标记参数

目前为止，我们只是看到了带有位置参数的部分应用，但是它和标记参数也能有很好的结合。再次考虑一下标记参数版本的`add`函数：

```OCaml
    # let add = (~x, ~y) => x + y;
    let add: (~x: int, ~y: int) => int = <fun>;
```

如果我们调用`add`的时候只传第一个标记参数，我们将得到一个新的函数作为返回值，并且这个函数的参数对应了`add`第二个参数：

```OCaml
    # add(~x=4);
    - : (~y: int) => int = <fun>
```

类似地，如果我们只提供第二个标记参数：

```OCaml
    # add(~y=4);
    - : (~x: int) => int = <fun>
```

也就是说，标记函数的部分应用不要求参数顺序。由于你可以部分应用任何一个标记参数，而不仅仅是最后一个，所以这也意味着部分应用和标记函数结合更加的通用。

#### 部分应用和可选参数

部分应用和可选参数在一起呢？下面的这个`add`函数只有可选参数：

```OCaml
    # let add = (~x=0, ~y=0, ()) => x + y;
    let add: (~x: int=?, ~y: int=?, unit) => int = <fun>;
```

如果只提及标记参数`~x`和`~y`，部分应用和之前一样的正常运行，但是一个不同：附加的`unit`参数也必须被填充。

```OCaml
    # add(~x=3);
    - : (~y: int=?, unit) => int = <fun>
    # add(~y=3);
    - : (~x: int=?, unit) => int = <fun>
    // 自己附加的命令行
    # add()()();
    - : int = 6
```

但是，一旦有了位置参数，就没有部分应用了，这个默认值将被填入：

```OCaml
    # add(~x=3, ());
    - : int = 3
    # add(~y=3, ());
    - : int = 3
```

即使你运行了一个或两个中间步骤，但是`()`始终是结束信号。其中运行了一个中间步骤的例子如下所示：

```OCaml
    # let plus5 = add(~x=5);
    let plus5: (~y: int=?, unit) => int = <fun>;
    # plus5(());
    - : int = 5
```

两个中间步骤：

```OCaml
    # let plus5 = add(~x=5);
    let plus5: (~y: int=?, unit) => int = <fun>;
    # let result8 = plus5(~y=3);
    let result8: (unit) => int = <fun>;
    # result8(());
    - : int = 8
```

#### 柯里化（高级）

柯里化是实现位置参数部分应用的一种技术，函数的柯里化就是将一个一元或多元函数转化为一系列的一元函数调用。

例如，二元函数`add`：

```OCaml
    let add = (x, y) => x + y;
```

`add`函数进行柯里化就是将其转换为如下格式：

```OCaml
    let add = x => y => x + y;
```

那么，我们不得不如下方式来调用`add`：

```OCaml
    # add(3)(1);
    - : int = 4
```

我们得到了是么？部分应用很容易实现：

```OCaml
    # let plus4 = add(4);
    let plus4: (int) => int = <fun>;
    # plus4(7);
    - : int = 11
```

并且现在令人惊讶的是：ReasonML 的所有函数的柯里化都会自动生效，这就是它为什么能支持部分应用的原因。如果你要以下看柯里化后函数`add`的类型：

```OCaml
    # let add = x => y => x + y;
    let add: (int, int) => int = <fun>;
```

换句话说，`add(x, y)`和`add(x)(y)`是和他们的类型都是等价的：

```OCaml
    (int, int) => int
    int => int => int
```

让我们以一个二元函数的柯里化为例来结束话题。考虑到柯里化一个已经柯里化过的函数是没有意义的，我们柯里化一个一对单一参数的函数。

```OCaml
    let curry2 = (f: (('a, 'b)) => 'c) => x => y => f((x, y));
```

让我们使用`curry2`和一元函数`add`：

```OCaml
    # let add = ((x, y)) => x + y;
    let add: ((int, int)) => int = <fun>;
    # curry2(add);
    - : (int, int) => int = <fun>
```

这个输出结果的类型告诉我们，我们已经创建了一个二元函数。

### 反转应用操作符（|>）

运算符`|>`叫做*反向应用运算符*或者*管道运算符*，它允许你链式函数调用：`x |> f`和`f(x)`相同。这个也许看起起来没有太多的不同，但是当组合函数调用时非常的有用。

#### 示例：整数和字符串应用管道

让我们从一个简单的例子开始，给出以下两个函数：

```OCaml
    let times2 = (x: int) => x * 2;
    let twice = (s: string) => s ++ s;
```

如果我们按照传统函数调用方式进行使用，我们得到：

```OCaml
    # twice(string_of_int(times2(4)));
    - : string = "88"
```

首先我们使用参数`4`调用函数`times2`，然后使用`string_of_in`（标准库中类型转换函数）处理其运行结果等。管道操作符允许我们写出更接近于刚才描述的代码：

```OCaml
    let result = 4 |> times2 |> string_of_int |> twice;
```

#### list 应用管道

随着更复杂的数据和柯里化，我们得到了一种让人联想到面向对象编程中的链式调用的编码风格。

例如，以下代码中的整型 list 可以很好的运行：

```OCaml
    [4, 2, 1, 3, 5]
    |> List.map(x => x + 1)
    |> List.filter(x => x < 5)
    |> List.sort(compare);
```

这些功能将在[list 章节][lists_chapter]进行解释，现在，我们已经对它的工作原理有了充分的了解了。

这三步计算分别是：

```OCaml
    # let l0 = [4, 2, 1, 3, 5];
    let l0: list(int) = [4, 2, 1, 3, 5];
    # let l1 = List.map(x => x + 1, l0);
    let l1: list(int) = [5, 3, 2, 4, 6];
    # let l2 = List.filter(x => x < 5, l1);
    let l2: list(int) = [3, 2, 4];
    # let l3 = List.sort(compare, l2);
    let l3: list(int) = [2, 3, 4];
```

我们看到，再这些所有的函数中，主参数都位于参数的最后一个。当我们使用管道时，我们总是把部分应用作为下一个函数的第一个参数，然后管道操操作符通过调该函数填充主参数。

这个主参数与面向对象语言中的`this`或`self`相似。

### 设计函数签名技巧

这里有一些设计函数签名的技巧：

* 如果函数只有单一主参数，则将其设计为最后一个位置参数，它可以很好使用管道进行函数组合。

* 一些函数有多个相似的主参数，则将他们设计为最后多个位置参数。举一个例子，一个函数需要把两个 list 拼接成一个，则需要把两个 list 都设计为位置参数。

* 其他的所有参数都应该是标记参数。

* 如果有两个或多个不同的主参数，则所有参数都应该被标记。

* 如果一个函数只有一个参数，它常常不是标记参数，即使它连严格的主参数都不是。

这个规则背后的思想是使代码尽可能的实现自我描述：这个主（或者唯一）参数通过函数名来描述，其他参数则通过他们的标签来描述。

函数一旦有了一个以上的位置参数，通常会变得很难描述每个参数的作用。例如，比较以下两个函数调用，第二个更加容易理解：

```OCaml
    blit(bytes, 0, bytes, 10, 10);
    blit(~src=bytes, ~src_pos=0, ~dst=bytes, ~dst_pos=10, ~len=10);
```

由于可选参数能够在不中断现有函数调用的情况下向函数添加更多的参数，所以我也喜欢可选参数，这有助于 API 的演变。

本章节来源：Sect.的《OCaml 手册》中的 [Suggestions for labeling（标记参数建议）][ocaml_label] 章节。

### 单参数匹配函数

ReasonML 提供了一元函数的缩写来立即`switch`他们的参数，如下例：

```OCaml
    let divTuple = (tuple) =>
      switch tuple {
      | (_, 0) => (-1)
      | (x, y) => x / y
      };
```

这个函数如下进行调用：

```OCaml
    # divTuple((9, 3));
    - : int = 3
    # divTuple((9, 0));
    - : int = -1
```

如果你使用`fun`操作符来定义`devTuple`函数，代码将变得更加简短：

```OCaml
    let divTuple =
      fun
      | (\_, 0) => (-1)
      | (x, y) => x / y;
```

### （高级）

其他部分都是高级应用方式。

### 运算符

ReasonML 有一个非常巧妙的特征，运算符也算函数。如果把他们放到小括号里，你可以像使用函数一样使用他们。

```OCaml
    # (+)(7, 1);
    - : int = 8
```

你也可以定义你自己的运算符：

```OCaml
    # let (+++) = (s, t) => s ++ " " ++ t;
    let ( +++ ): (string, string) => string = <fun>;
    # "hello" +++ "world";
    - : string = "hello world"
```

把运算符插入到小括号里，也可以很容易的查找其类型：

```OCaml
    # (++);
    - : (string, string) => string = <fun>
```

#### 运算符规则

有两种运算符：中缀运算符（在两个操作数之间）和前缀运算符（在单个操作数之前）。

下列字符可用于这两种运算符：

```OCaml
    ! $ % & * + - . / : < = > ? @ ^ | ~
```

**中缀运算符：**

| First character            | Followed by operator characters |
| -------------------------- | ------------------------------- |
| = < > @ ^ ❘ & + - \* / $ % | 0+                              |
| #                          | 1+                              |

另外，以下关键字是中缀运算符：

    * + - -. == != < > || && mod land lor lxor lsl lsr asr

**前缀运算符：**

| First character | Followed by operator characters |
| --------------- | ------------------------------- |
| !               | 0+                              |
| ? ~             | 1+                              |

另外，以下关键字是前缀运算符：

    - -.

本章节来源：Sect.的《OCaml 手册》中的 [Prefix and infix symbols（中缀和前缀符号）][ocaml_infixsymbol] 章节。

#### 运算符的先导性与关联性

下表列出了运算符及其关联性，其优先级（越高绑定越强）从上往下依次降低，比如，`*`比`+`的优先级高。

| 构造函数或运算符                  | 优先级 |
| --------------------------------- | ------ |
| prefix operator                   | –      |
| . .( .[ .{                        | –      |
| [ ] (array index)                 | –      |
| #···                              | –      |
| applications, assert, lazy        | left   |
| - -. (prefix)                     | –      |
| \*\*··· lsl lsr asr               | right  |
| \*··· /··· %··· mod land lor lxor | left   |
| +··· -···                         | left   |
| @··· ^···                         | right  |
| =··· <··· >··· ❘··· &··· $··· !=  | left   |
| &&                                | right  |
| ❘❘                                | right  |
| if                                | –      |
| let switch fun try                | –      |

说明：

* `op...`表示其后跟着其他操作符
* `Applications`：函数应用、构造函数应用、标签应用

本章节来源：Sect.的《OCaml 手册》中的 [Expressions（表达式）][ocaml_expression] 章节。

#### 什么时候关联性重要？

每当运算符是一个不可交换运算符的时候，关联性就显得很重要。对于一个可交换运算符，操作数的顺序不重要。例如，加法（+）是可交换运算符，然而减法（-）不是的。[[注 2](#note_2)]

左关联性意味着运算符从左往右进行分组，如下两个表达式是等价的：

```OCaml
    x op y op z
    (x op y) op z
```

减法（-）是左关联性运算符：

```OCaml
    # 3 - 2 - 1;
    - : int = 0
```

右关联性意味着运算符从右往左进行分组，如下两个表达式是等价的：

```OCaml
    x op y op z
    x op (y op z)
```

我们可以自定义一个右关联的负号运算符，根据运算表，从@符号开始，它是自动右关联的：

```OCaml
    let (@-) = (x, y) => x - y;
```

如果我们使用这个运算符，我们可以得到一个不同于寻常减法的结果：

```OCaml
    # 3 @- 2 @- 1;
    - : int = 2
```

### 函数的多态

回想一下*多态（polymorphism）*的定义：对多种类型进行相同的操作。有多种方式可以实现多态，面向对象语言是通过*子类（subclassing）*来实现，*重载（overloading）*也是一种实现多态的方式。

ReasonML 支持*参数多态（parametric polymorphism）*：所谓*类型变量（type variables）*表明可以填充任何类型。（这些变量是普遍量化的。）使用类型变量的函数被称作*泛型函数（generic function）*。

#### 示例：`id()`

例如，`id`是一个简单返回其参数的*标识函数（identity function）*：

```OCaml
    # let id = x => x;
    let id: ('a) => 'a = <fun>;
```

在 ReasonML 里，`id`的类型推断是很有趣的：它不能检测到`x`的类型，因此它使用变量类型`'a`来表示“任何类型”。类型变量总是以一个直撇号开始。ReasonML 还会推断返回值的类型和参数类型一致。这是有用信息。

`id`是通用的并且对于任何类型都可用：

```OCaml
    # id(123);
    - : int = 123
    # id("abc");
    - : string = "abc"
```

#### 示例：`first()`

再让我们看另外一个例子：通用函数`first`用于获取一对数据（一个二元组）第一个组件。

```OCaml
    # let first = ((x, y)) => x;
    let first: (('a, 'b)) => 'a = <fun>;
```

`first`用于分解获取元组的第一个组件。类型推断告诉我们，返回值数据类型与第一个组件的类型相同。

我们可以使用下划线来表示我们对第二个组件不感兴趣：

```OCaml
    # let first = ((x, _)) => x;
    let first: (('a, 'b)) => 'a = <fun>;
```

使用类型注解组件后`first`函数如下所示：

```OCaml
    # let first = ((x: 'a, _)) => x;
    let first: (('a, 'b)) => 'a = <fun>;
```

#### 示例：`ListLabels.map()`

作为快速预览，这里介绍了另外一个函数签名，其会在[list 章节][lists_chapter]很好的进行解释。

```OCaml
    ListLabels.map: (~f: ('a) => 'b, list('a)) => list('b)
```

#### 重载 vs. 参数多态

注意重载和参数多态的不同点：

* 重载为操作提供了不通的实现方式。例如，一些变成语言中使用`+`可以进行数值计算，字符串拼接，并且/或者数组拼接。

* 参数多态指定了一种适用于多种类型的单一算法。

### ReasonML 不支持变函数

ReasonML 不支持变函数（可变参数），也就是说，你不能定义函数来计算任意数量参数的和：

```OCaml
    let sum = (x0, ···, xn) => x0 + ··· + xn;
```

相反，你必须为每种不同参数数量定义一个函数：

```OCaml
    let sum2(a: int, b: int) = a + b;
    let sum3(a: int, b: int, c: int) = a + b + c;
    let sum4(a: int, b: int, c: int, d: int) = a + b + c + d;
```

你已经看到了一个类似于柯里化的技术，这里我们不能定义一个变函数`curry()`，相反，必须使用二元函数`curry2()`。你也将在这一系列丛书中偶尔见到。

另一种解决方案是使用整型 list。

<span id="note_1">[注 1]</span> 类似于函数式编程的*柯里化*，只不过 ReasonML 是参数填充不足时自动进行。
<span id="note_2">[注 2]</span> 可以当作交换率理解，加法有交换率，但是减法没有。

[阅读原文][origin_url]
