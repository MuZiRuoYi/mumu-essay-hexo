---
title: 探索ReasonML_11 - list
date: 2018-08-12 11:19:34
tags: [ReasonML]
category: "reasonml"
---

[origin_url]: http://reasonmlhub.com/exploring-reasonml/ch_lists.html
[list_api]: https://reasonml.github.io/api/List.html
[listlabels_api]: https://reasonml.github.io/api/ListLabels.html
[stdlabels_api]: https://reasonml.github.io/api/StdLabels.html
[ch_recursion]: http://reasonmlhub.com/exploring-reasonml/ch_recursion.html

在此章节，看一下 ReasonML 数据结构*list*。

list 是具有相同类型元素的序列，其数据结构不可改变。其特别适合通过模式匹配进行处理。

### 简短离题：类型签名

本章节中，将要看到许多如下类型签名：

    let map: ('a => 'b, list('a)) => list('b);

类型签名也许看起来仍是神秘的，但是将会从如何阅读他们当中受益。让我们探索一下类型签名将要告诉我们一些关于`map`的什么信息——也许我们还不知道`map`是什么或者是做什么的（稍后将要解释）。探索到：

- `let map: («parameters») => «result»;`
  `map`是一个函数。

- `'a => 'b`
  `map`的第一个参数是从类型`'a`到类型`'b`的转换函数。这个前缀撇号表示`a`和`b`是*类型变量*：他们接受任何类型，但是一旦接受了莫个特定类型，那么从此之后就只能接受该类型。

- `list('a)`
  `map`的第二个参数是元素类型为`'a`的 list。

- `list('b)`
  `map`的返回值是一个元素类型为`'b`的 list。

### list 和数组的标准库功能

list 的原始标准库模块是[List][list_api]，具有如下功能：

    let map: ('a => 'b, list('a)) => list('b);

List 模块在一个标记的版本[ListLabels][listlabels_api]也是可用的，具有如下功能：

    let map: (~f: 'a => 'b, list('a)) => list('b);

也可以通过打开模块[StdLabels][stdlabels_api]，使用一些技巧并且”替换“几个正常模块及其标记的版本：

    open StdLabels;

该模块有`Array`，`Bytes`，`List`和`String`子模块，是全局 ArrayLabels 等模块的别名。所以，如果打开它，将要覆盖 Array 等的未标记的实现。

### list 的结构

#### 通过自递归参数化变量的 list

list 是一个自递归参数化变量。如果要自己定义，那么就是这个样子：

```OCaml
type mylist('a) =
  | Nil;
  | Cons('a, mylist('a))
```

`Nail`和`Cons`（“结构体”）是有根据的，起源于 Lisp 程序设计语言。通过嵌套 Cons 创建 list：

    # let abc = Cons("a", Cons("b", Cons("c", Nil)));
    let abc: mylist(string) = Cons("a", Cons("b", Cons("c", Nil)));

`mylist`有类型参数`'a`，它被传递给`Cons`，并且被`mylist`递归调用。这意味着两件事情：第一，`mylist`元素可以有任何类型。第二，他们必须有同样的类型。在之前与 ReasonML 交互中，可以看到自动推断出了`abc`，`'a`是字符串。

`abc`是一个单链表，在内存中，它看起来可能是这样的：

![Alt text](/img/singly_linked_list.svg "abc单链表内存关系")

`cons`的两部分被成为：

- _Head（或 first）_：它是当前 list 的第一个值。
- _Tail (or rest)_：它指向另一个带有剩余元素的列表。

#### 创建和模式匹配 ReasonML 的 list

ReasonML list 有特殊语法，这两个构造函数是：

- 空 list []。
- cons 对[head, ...tail]。

因此，模式匹配如下工作：

```OCaml
switch myList {
| [] => ···
| [head, ...tail] => ···
}
```

根据上一节，这是重新创建 list`abc`的一种方式：

    # let abc = ["a", ...["b", ...["c", ...[]]]];
    let abc: list(string) = ["a", "b", "c"];

可以看到 rtop 建议的等价的更加紧凑的语法：

    # let abc = ["a", "b", "c"];

let abc: list(string) = ["a", "b", "c"];

通过模式匹配计算任意 list 类型的 myList 的长度：

```OCaml
let rec len = (myList: list('a)) =>
  switch myList {
  | [] => 0
  | [_, ...tail] => 1 + len(tail)
  };
```

对两个构造行数进行递归：

- 空的 list 长度为 0。
- 不空 list 的长度：tail 的长度加 1。

类型参数`'a`使`len`变成泛型函数。但是我们对元素的类型从不感兴趣，只关注 list 的结构。如下`len`适用于各种 list：

    # len([]);
    - : int = 0
    # len(["a", "b"]);
    - : int = 2
    # len([1, 2, 3, 4]);
    - : int = 4

#### 打印 list

ReasonML 没有内置支持复杂数据类型的打印，但是 BuckleScript 可以帮助使用 JavaScript 的`console.log()`。最好如下使用打印函数：

    Js.log(Array.of_list(myList));

在打印 list`myList`之前，我们为什么转换它为数组？为了更好的输出。因为 ReasonML 的 list 在 JavaScript 表示为嵌套的两个元素的数组（一对 cons 编码）。

#### 创建 list 的更多方法

“三个点“构造函数（triple dot constructor）也叫做*展开操作符（spread operator）*。这个操作符允许在存在的 list 前准备 0 个或多个元素：

    # [...["a", "b"]];
    - : list(string) = ["a", "b"]
    # ["a", ...["b", "c"]];
    - : list(string) = ["a", "b", "c"]
    # ["a", "b", ...["c", "d"]];
    - : list(string) = ["a", "b", "c", "d"]

唉，这个操作符仅在末尾起作用，在 JavaScript 中，你可以在任何位置使用，但是 ReasonML 中不能：

    # [...["a", "b"], ...["c", "d"]];
    Error: Syntax error

ReasonML 有它自己的操作符，用于把 list 连接起来：

    # ["a", "b"] @ ["c", "d"];
    - : list(string) = ["a", "b", "c", "d"]

请注意，由于需要考虑到从第一个操作数到第二个操作数的所有元素，所以连接 list 操作是比较慢的。

```OCaml
let rec append = (l1: list('a), l2: list('a)) =>
  switch l1 {
  | [] => l2
  | [head, ...tail] => [head, ...append(tail, l2)]
  };
```

以下方式使用`append`：

    # append([1,2,3], [4,5]);
    - : list(int) = [1, 2, 3, 4, 5]

这又是类型推断在起作用：

- 首先，ReasonML 推断出了`1`等数据的数据类型，是 int。

- 然后它推断出了例如第一个输入 list 的数据类型`list(int)`。

        # [1,2,3];
        list(int) = [1, 2, 3]

- 然后检查`l1`he`l2`的`'a`参数是否拥有相同的数据类型。

- 最后，根据`l1`和`l2`的数据类型推断出`append`返回值的数据类型`list(int)`。

### 示例：创建 list

`range()`创建一个 int 类型的 list:

```OCaml
/**
 * Compute a list of integers starting with `start`,
 * up to and excluding `end_`.
 */
let rec range = (start: int, end_: int) =>
  if (start >= end_) {
    [];
  } else {
    [start, ...range(start + 1, end_)];
  };
```

由于`end`是 ReasonML 的一个关键字，因此不是一个合法的变量名。这也是为什么使用`end_`，含有下划线，作为变量名。尝试使用`range()`：

    # range(0, 0);
    - : list(int) = []
    # range(0, 1);
    - : list(int) = [0]
    # range(0, 5);
    - : list(int) = [0, 1, 2, 3, 4]

`fill()`创建一个填充值`~element`的 list：

```OCaml
/**
 * Create a list of length `~length` where each
 * element is `~element`.
 */
let rec fill = (~element: 'a, ~length: int) =>
  if (length <= 0) {
    [];
  } else {
    [element, ...fill(~element, ~length=length-1)];
  };
```

ReasonML 使用`~element`的类型推断返回值的类型：

    # fill("x", 4);
    - : list(string) = ["x", "x", "x", "x"]
    # fill(0, 3);
    - : list(int) = [0, 0, 0]

### 示例：读取 list

#### 计算 list 的数据和

`summarize()`计算出一个 list 中所有 int 类型数据的和：

```OCaml
/**
 * Compute the sum of all the ints in the list `l`.
 */
let rec summarize = (l: list(int)) =>
  switch l {
  | [] => 0
  | [head, ...tail] => head + summarize(tail)
  };

summarize([]); /* 0 */
summarize([3]); /* 3 */
summarize([1, 2, 3]); /* 6 */
```

#### 访问 list 的 n-th 元素：

`getElementAt()`通过索引获取 list 的一个元素：

```OCaml
/**
 * Get the list element at index `~index`.
 * The head of a list has the index 0,
 * the head of its tail the index 1, etc.
 */
let rec getElementAt = (~index: int, l: list('a)) =>
  switch l {
  | [] => None
  | [head, ...tail] =>
    if (index <= 0) {
      Some(head);
    } else {
      getElementAt(~index=index-1, tail);
    }
  };
```

可以使用一个`when`从句和一个带有`case`的`switch`语句来淘汰掉`if-then-else`表达式。由此产生的扁平结构更易阅读：

```OCaml
let rec getElementAt = (~index: int, l: list('a)) =>
  switch l {
  | [] => None
  | [head, ..._] when index <= 0 => Some(head)
  | [head, ...tail] => getElementAt(~index=index-1, tail)
  };
```

这段代码中有些值得注意的地方：

- 通过 variant 变量选项来处理失败：

  - `None`意味着失败。

  - `Some(x)`意味着已经成功，返回值是`x`。

- 索引`0`是指当前的头部（2nd case）。

- 如果遍历到了 list 的` end``[] `，我们就失败了。如果`l`是空或者在`~index`是`0`之前到达了`end`，触发`switch`的第一个`case`。

标准库[ListLabels.nth()][listlabels_api]中有些与`getElementAt()`类似的方法，但是它为非法索引会抛出异常，而不是作为一个选项来处理。

### 示例：改变 list

考虑到给定的 list 是不可改变的——如何去改变他们，为了找到答案，仔细思考，迄今为止，我们已经看到了两种类型的算法：

- 通过递归 list 的数据结构来读取数据，例如`len()`，`summarize()`等。
- 通过递归构建一个新的数据结构来创建一个 list。例如：`range()`，`fill()`等。

我们结合这两种方式来改变列表：我们创建一个全新的 list，并且包括现有 list 的数据或者从现有 list 的数据中派生数据。

#### removeAll()

以下是更改 list 的第一个示例：

```OCaml
/**
 * Remove all elements from the list `l` that are
 * equal to `~value`.
 */
let rec removeAll = (~value: 'a, l: list('a)) =>
  switch l {
  | [] => []
  | [head, ...tail] when head == value => removeAll(~value, tail)
  | [head, ...tail] => [head, ...removeAll(~value, tail)]
  };
```

第一个`case`意味着我们已经完成，第二个`case`是对现有列表的一个精确复制，第三个`case`删除等于`~value`的元素。

如下使用`removeAll()`：

    # removeAll(~value=9, [1,9,2,9,3]);
    - : list(int) = [1, 2, 3]

#### replaceAll()

`replaceAll()`替换值：

```OCaml
/**
 * Inside the list `l`, remove all occurrences of the value `~value`
 * with the value `~with_`.
 */
let rec replaceAll = (~value: 'a, ~with_: 'a, l: list('a)) =>
  switch l {
  | [] => []
  | [head, ...tail] when head == value =>
    [with_, ...replaceAll(~value, ~with_, tail)]
  | [head, ...tail] =>
    [head, ...replaceAll(~value, ~with_, tail)]
  };
```

第一个`case`意味着已经完成，第二个`case`做精确复制，第三个`case`进行替换。

我们可以通过添加内部辅助函数`replaceOne()`使`replaceAll()`的结构更加紧凑：

```OCaml
let rec replaceAll = (~value: 'a, ~with_: 'a, l: list('a)) => {
  let replaceOne = (x) => if (x == value) with_ else x;
  switch l {
  | [] => []
  | [head, ...tail] =>
    [replaceOne(head), ...replaceAll(~value, ~with_, tail)]
  };
};
```

如下使用`replaceAll()`：

    # replaceAll(~value=1, ~with_=11, [1, 2, 1, 3]);
    - : list(int) = [11, 2, 11, 3]

#### drop()

`drop()`移除 list 元素：

```OCaml
/**
 * Remove the first `~count` elements of `theList`.
 */
let rec drop = (~count, theList: list('a)) =>
  switch theList {
  | [] => []
  | l when count <= 0 => l
  | [_, ...tail] => drop(~count=count-1, tail)
  };
```

使用`drop()`：

    # drop(~count=0, ["a", "b", "c", "d"]);
    - : list(string) = ["a", "b", "c", "d"]
    # drop(~count=2, ["a", "b", "c", "d"]);
    - : list(string) = ["c", "d"]
    # drop(~count=2, ["a", "b"]);
    - : list(string) = []
    # drop(~count=2, ["a"]);
    - : list(string) = []
    # drop(~count=2, []);
    - : list('a) = []

对于`drop()`的最后一个返回结果，ReasonML 无法推断出元素类型并且丢弃未绑定的参数类型`'a`。

### list 标准函数库

ReasonML 的标准函数库仍在不断的变化，因此我们仅仅关注几个亮点。可以通过[ListLabels 文档][listlabels_api]阅读剩余部分（如果它当前还存在的话）。

#### ListLabels.map()

签名：

    let map: (~f: 'a => 'b, list('a)) => list('b);

`map()`接受一个元素类型为`'a`的 list，每一个元素都调用函数~f 并且返回值组成一个新的 list。

    # ListLabels.map(~f=x => int_of_string(x), ["7", "15", "6"]);
    - : list(int) = [7, 15, 6]

这个是处理 list 数据的经典工具函数。

`mapi()`是`map()`的一个版本，将当前元素和当前元素的索引传递给毁掉函数`~f`。我们可以使用`mapi()`来非破坏性的更新 list：

```OCaml
/**
 * Create a copy of `theList` whose element at index `~index`
 * is `~value`.
let setElementAt = (~index: int, ~value: 'a, theList: list('a)) =>
  ListLabels.mapi(
    ~f=(i,x) => if (i==index) value else x,
    theList
  );
```

除了索引`index`等于`x`的元素，其他不变元素，通过参数`~f`传递。

如下使用`setElementAt()`：

    # setElementAt(~index=1, ~value="|", ["a", "b", "c"]);
    - : list(string) = ["a", "|", "c"]

#### ListLabels.filter()

这个函数签名：

    let filter: (~f: 'a => bool, list('a)) => list('a);

`filter()`接受一个函数`~f`为参数，并且 list 每个元素使用该函数，如果返回`true`，则返回值中包含该元素，如果返回`false`，则这个元素不在返回值中，如下可以使用该函数。

    # ListLabels.filter(~f=x=>x>5, [8, 4, 9, 7, 2]);
    - : list(int) = [8, 9, 7]

#### ListLabels.for_all()

签名：

    let for_all: (~f: 'a => bool, list('a)) => bool;

list 的每一个元素使用`~f`函数，如果都返回`true`则`for_all()`返回`true`，例如：

    # ListLabels.for_all(~f=x=>x>3, [4,5,6]);
    - : bool = true
    # ListLabels.for_all(~f=x=>x>3, [3,4,5,6]);
    - : bool = false

只要有`~f`返回`false`，则`for_all`停止运行，然后返回值必定为`false`。`for_all`是以数学运算符 ∀ 命名的。

`ListLabels.exists()`和`for_all()`有关：如果他的元素至少有一元素返回`true`，则其返回值为`true`。`exists`是以数学符号 ∃ 命名的。

#### ListLabels.flatten()

签名：

    let flatten: list(list('a)) => list('a);

`flatten()`通过把一个 list 类型元素组成的 list`l`的元素连接起来，生成一个新的 list。也就是说，如下三个表达式是等价的：

      flatten([l1, l2, l3])
      ListLabels.append(l1, ListLabels.append(l2, l3))
      l1 @ l2 @ l3

如下使用`flatten()`：

    # ListLabels.flatten([[1,2], [], [3,4,5]]);
    - : list(int) = [1, 2, 3, 4, 5]

如果对 list 的嵌套感到困惑，回忆一下，ReasonML 中的 list 必须所有元素具有相同的类型，因此，如果一个 list 元素本身是 list，那么所有元素都是 list：

    # ListLabels.flatten([[1,[2]], [], [3]]);
    Error: This expression has type list('a)
    but an expression was expected of type int
    # ListLabels.flatten([[[1],[2]], [], [[3]]]);
    - : list(list(int)) = [[1], [2], [3]]

让我们继续看一下`flatten()`的用例：

1.  用例：同时过滤和映射

`flatten()`可以使`filter`和`map`同时使用。例如，多个 list 组成一个新的 list（即 list 的每一个元素又是一个 list），试图读取每一个子 list 的第一个元素，可以：

- 首先使用`ListLabels.filter()`过滤掉空 list 元素（没有任何一个元素的 list）。
- 然后使用`ListLabels.map()`映射出每一个非空 list 的 head。

或者，可以使用`flatten`同时做到两者：

```OCaml
module L = ListLabels;
let listFromHead = (l: list('a)) =>
  switch (l) {
  | [] => []
  | [head, ..._] => [head]
  };
let heads = (l: list(list('a))) =>
  L.flatten(L.map(~f=listFromHead, l));
```

首先我们把非空 list 映射出一个仅有 head 的 list，空 list 映射到一个空 list，然后把这个结果扁平化，如下所示：

    # let l0 = [[1, 2], [], [3,4,5]];
    let l0: list(list(int)) = [[1, 2], [], [3, 4, 5]];
    # L.map(~f=listFromHead, l0);
    - : list(list(int)) = [[1], [], [3]]
    # let l1 = L.map(~f=listFromHead, l0);
    let l1: list(list(int)) = [[1], [], [3]];
    # L.flatten(l1);
    - : list(int) = [1, 3]

这些步骤相当于：

    # heads([[1, 2], [], [3,4,5]]);
    - : list(int) = [1, 3]

将`listFromHead`和`getHead`函数相比较是有意义的，`getHead`中使用一个选项（option）来表示失败：

```OCaml
let getHead = (l: list('a)) =>
  switch (l) {
  | [] => None
  | [head, ..._] => Some(head)
  };
```

也就是说，`None`表示“`l`没有 head”：

    # getHead(["a", "b"]);
    - : option(string) = Some("a")
    # getHead([1, 2, 3]);
    - : option(int) = Some(1)
    # getHead([]);
    - : option('a) = None

对于`listFromHead`，我们使用空的 list 代替`None`，使用单元素的 list 代替`Some`。

2. 用例：映射到多个值

假设我们已经创建了一个 people 和他们 children 的 list：

```OCaml
type person = Person(string, list(string));
let persons = [
  Person("Daisy", []),
  Person("Della", ["Huey", "Dewey", "Louie"]),
  Person("Marcus", ["Minnie"])
];
```

如果想要手机 list 中的所有 children，`ListLabels.map()`几乎给我们提供了所有我们需要的，但是并不是完全提供：

    # ListLabels.map(~f=(Person(_, ch)) => ch, persons);
    - : list(list(string)) = [[], ["Huey", "Dewey", "Louie"], ["Minnie"]]

唉，这是一个字符串 list 组成的 list，而不是一个字符串的 list。可以使用`ListLabels.flatten()`修正这个嵌套的 list：

```OCaml
let collectChildren = (persons: list(person)) =>
  ListLabels.flatten(
    ListLabels.map(
        ~f=(Person(_, children)) => children,
        persons));

collectChildren(persons);
  /* ["Huey", "Dewey", "Louie", "Minnie"] */
```

现在，得到了预期的结果。

    # collectChildren(persons);
    - : list(string) = ["Huey", "Dewey", "Louie", "Minnie"]

3. 用例：有条件的插入值

有时候，想要创建一个某些条件（下例中的`cond`）添加或忽略某些元素的 list：

```OCaml
let func = (cond: bool) => ListLabels.flatten([
  if (cond) ["a"] else [],
  [
    "b",
    "c"
  ]
]);
```

如下使用`func()`：

    # func(true);
    - : list(string) = ["a", "b", "c"]
    # func(false);
    - : list(string) = ["b", "c"]

#### ListLabels.fold_left()

签名：

    let fold_left: (~f: ('a, 'b) => 'a, ~init: 'a, list('b)) => 'a;

`fold_left()`如下方式运行：

- 输入：一个类型为`list('b)`（最后一个参数）的 list
- 返回值：一直类型为`'a`的值

`fold_left()`依赖它的参数`~f`函数计算结果，为它的输入 list 每个元素调用`~f`：

    let nextIntermediateResult = f(intermediateResult, elem);

`intermediateResult`是已经被计算后的结果，第一个中间结果是`~init`，最后的`nextIntermediateResult`是`fold_left()`的返回值。

让我们来看一个具体的事例。

1. `fold_left()`: `summarize()`例子

在计算 int 型 list 总和的时候，已经遇到过`summarize()`函数了，让我们通过`fold_left()`实现这个函数：

```OCaml
let rec summarize = (l: list(int)) =>
  ListLabels.fold_left(~f=(r, elem) => r + elem, ~init=0, l);
```

要理解`summarize()`如何运行，请考虑下面的表达式：

```OCaml
summarize([1,2,3]) /* 6 */
```

为了计算结果`6`，采用以下步骤：

```OCaml
/* Parameter */
let f = (r, elem) => r + elem;
let init = 0;

/* Steps */
let result0 = f(init, 1); /* 1 */
let result1 = f(result0, 2); /* 3 */
let result2 = f(result1, 3); /* 6 */
```

`result2`就是`fold_left()`的返回值。

2. 另一种查看`fold_left()`的方法

另一种查看`fold_left()`的方法是使用二元运算符`~f`并且将其转换为用于 list 的 n 元运算符。从二元运算符`+`转成 n 元运算符（运算符`Σ`）在数学中是存在示例的，`summarize()`就是从`+`到`Σ`。它也可以这样写：

    # ListLabels.fold_left(~f=(+), ~init=0, [1, 2, 3]);
    - : int = 6

我发现在这种模式下运行——带一个*可交换的*`~f`（参数顺序无关紧要）——`fold_left`最好理解。但是你可以用它做很多事情——阅读下面的例子：

3. 一个更复杂的例子：查找值

使用`contains()`函数去查找 list 中的值：

```OCaml
let contains = (~value: 'a, theList: list('a)) => {
  let f = (found, elem) => found || elem == value;
  fold_left(~f, ~init=false, theList);
};
```

#### 通过`iteri()`将 list 转成数组

签名：

    let iteri: (~f: (int, 'a) => unit, list('a)) => unit;

`iteri()`中，第二个参数 list 的每个元素调用函数`~f`，其参数分别是元素的索引和元素本身。它的返回值`unit`意味着`iteri`是通过副作用使每一次运算有意义的。

下面函数使用`iteri()`来填充数组，通过写入数组`arr`也是一种副作用。

```OCaml
let arrayFromList = (~init: 'a, l: list('a)) => {
  let arr = ArrayLabels.make(ListLabels.length(l), init);
  ListLabels.iteri(~f=(i, x) => arr[i]=x, l);
  arr;
};
```

由于`make()`的需要（稍后解释为什么），`~init`是一个必要参数。

使用`arrayFromList()`：

    # arrayFromList(~init=0, [1,2,3]);
    - : array(int) = [|1, 2, 3|]

### 接下来该看点什么

现在是阅读本书中[递归章节][ch_recursion]的好时机，将使我们对递归有更深的理解，并且也会告诉我们怎样使本章中的代码更有效【通过*尾部调用（tail calls）*】。

[阅读原文][origin_url]
